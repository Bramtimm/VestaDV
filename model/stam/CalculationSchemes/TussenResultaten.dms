//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container tussenresultaten
{
	#include <RekenStapT.dms>
	#include <BebouwingsComponentT.dms>
	#include <GebouwOptieT.dms>
	
	template DoelLabelT {
		unit<uint32> BebouwingsObject;
		parameter<Classifications/DoelLabel_NaSprong> DoelLabel_rel;
		
		unit<uint8>  SchilSprong_set := VerbruiksOpties/SchilSprong_set; // outward ref
		unit<uint32> SchilSprong_x_MO:= VerbruiksOpties/SchilSprong_set/xMO;
		
		attribute<Classifications/SchilLabel> Van_rel(BebouwingsObject) := BebouwingsObject/schillabel_rel;
		parameter<Classifications/SchilLabel> Naar_rel                  := Classifications/DoelLabel_NaSprong/SchilLabel_rel[DoelLabel_rel];
		
		attribute<SchilSprong_set > SchilSprong_rel    (BebouwingsObject) := rlookup(combine_data(Classifications/SchilSprongCode, Van_rel, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel(BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel, BebouwingsObject/Model_rel);
		
		attribute<EUR> Ki_GV (BebouwingsObject) := MakeDefined(
			SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel] * RuimtelijkeVraagComponent/nrAansluitingen +
			SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel] * RuimtelijkeVraagComponent/Oppervlakte    
		,	0[EUR]
		);
	}
	
	// TODO: heroverwegen per gebiedsoptie. Kijk eerst waar dit gebruikt wordt.
	template WarmteVraagT 
	{
		unit<uint32> BebouwingsObject;
		attribute<bool> KanGebiedsoptie (BebouwingsObject) := Classifications/WamteOptie/kanGebiedsOptie[BebouwingsObject/WarmteOptie_rel];
		
		container deelnemers
		{
			attribute<nrAansl>   NrAansluitingen    (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/nrAansluitingen;
			attribute<m2>        Oppervlakte        (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/Oppervlakte;
			attribute<kW>        AansluitCapaciteit (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/AansluitCapaciteit/Warmte;
			attribute<GJ_yr>     Verbruik           (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * Verbruik_RV + Verbruik_WW;
			attribute<GJ_yr>     Verbruik_RV        (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * results/Functioneel/Vj_RV;
			attribute<GJ_yr>     Verbruik_WW        (BebouwingsObject) := float64(KanGebiedsoptie) * RuimtelijkeVraagComponent/AandeelWarmteVraag * results/Functioneel/Vj_TW;
		}

		container alle
		{
			attribute<nrAansl>   NrAansluitingen    (BebouwingsObject) := RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/nrAansluitingen;
			attribute<kW>        AansluitCapaciteit (BebouwingsObject) := RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/AansluitCapaciteit/Warmte;
			attribute<GJ_yr>     Verbruik           (BebouwingsObject) := RuimtelijkeVraagComponent/AandeelWarmteVraag * results/Functioneel/Vj_RV + results/Functioneel/Vj_TW;
			attribute<m2>        Oppervlakte        (BebouwingsObject) := RuimtelijkeVraagComponent/AandeelWarmteVraag * RuimtelijkeVraagComponent/Oppervlakte;
		}
	}
}