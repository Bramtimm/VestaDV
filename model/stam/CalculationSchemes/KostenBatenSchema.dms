//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template KostenBaten
{
	// begin case parameters
	container bebouwing;
	// end case parameters
	unit<uint32> PlanRegio := Invoer/SpecifiekeInstellingen/PlanRegio;
	attribute<string> BCname(Classifications/BebouwingsComponent) := Classifications/BebouwingsComponent/name;
	
	container KanGebiedsOptie := for_each_nednv( BCname
		,	replace('float64(Classifications/WarmteOptie/KanGebiedsOptie[bebouwing/@BC/WarmteOptie_rel])', '@BC', BCname)
		,	bebouwing, BCname, float64
		);
	container WarmteVraag := for_each_ne(BCname, replace('CalculationSchemes/WarmteVraagT(bebouwing/@BC, KanGebiedsOptie/@BC)', '@BC', BCname));
	container Opbrengsten
	{
		container EenmaligD := for_each_nedv(BCname
			,replace('sum(Bebouwing/@BC@/Opbrengsten/AansluitBijdrageT * KanGebiedsOptie/@BC@, Bebouwing/@BC@/PlanRegio_rel)', '@BC@', BCname)
			,PlanRegio, Eur
			)
		{
			attribute<Eur> Oi_Totaal(PlanRegio) := ='add('+AsItemList(BCname)+')';
		}
		container JaarlijksD 
		{
			attribute<Eur_yr> Oj_VastRecht(PlanRegio) := ='add('+AsItemList('sum(WarmteVraag/'+BCname+'/Oj_VastRechtMT,      WarmteVraag/'+BCname+'/BO/PlanRegio_rel)' )+')';
			attribute<Eur_yr> Oj_Warmte   (PlanRegio) := ='add('+AsItemList('sum(WarmteVraag/'+BCname+'/Oj_Warmte,           WarmteVraag/'+BCname+'/BO/PlanRegio_rel)' )+')';
			attribute<Eur_yr> Oj_Totaal   (PlanRegio) := Oj_Warmte + Oj_VastRecht;
		}
	}
	container Kostenkentallen := Kengetallen/Kosten_MT(LeerCurves/MTnet/curve, LeerCurves/OenM/curve, LeerCurves/Inpand/curve);
	
	container KostenD
	{
		container Wonen
		{
			
			container bw := CalculationSchemes/KostenWoningen(bebouwing/BestaandeWoning, KanGebiedsOptie/BestaandeWoning, PlanRegio, false);
			container nw := CalculationSchemes/KostenWoningen(bebouwing/NieuwbouwWoning, KanGebiedsOptie/NieuwbouwWoning, PlanRegio, true);

			attribute<Eur>		Ki_wd     (PlanRegio)	:=	bw/ki_wd + nw/Ki_wd;
			attribute<Eur>		Ki_id     (PlanRegio)	:=	bw/ki_id + nw/Ki_id;
			attribute<Eur>		Ki_ge_ov  (PlanRegio)	:=	bw/ki_ge_ov + nw/Ki_ge_ov;
			attribute<Eur>		Ki_ge_pm  (PlanRegio)	:=	bw/ki_ge_pm + nw/Ki_ge_pm;
			attribute<Eur_yr>	Kj_wd_adm (PlanRegio)	:=	Ki_wd * Kengetallen/Onderhoud/R_wd_admin;
			attribute<Eur_yr>	Kj_wd_oh  (PlanRegio)	:=	Ki_wd * Kengetallen/Onderhoud/R_wd_onderhoud;
			attribute<Eur_yr>	Kj_id_adm (PlanRegio)	:=	Ki_id * Kengetallen/Onderhoud/R_id_admin;
			attribute<Eur_yr>	Kj_id_oh  (PlanRegio)	:=	Ki_id * Kengetallen/Onderhoud/R_id_onderhoud;
			attribute<Eur_yr>	Kj_ge_hv  (PlanRegio)	:=	bw/Kj_hv;
		}
		container Utiliteit
		{
			parameter<kW> afkapgrens_klein := 100[kW];

			container bu := UTILdata(WarmteVraag/BestaandeUtil/BO); //berekeningen op objectniveau bestaande glastuinbouw
			container nu := UTILdata(WarmteVraag/NieuwbouwUtil/BO); //berekeningen op objectniveau nieuwbouw glastuinbouw
			
			template UTILdata
			{
				unit<UInt32>       BO;
				
				// ========== oppervlak, aansluitingen en vermogensvraag
				attribute<m2>      bvo              (BO) := ='WarmteVraag/' + BO/BCname + '/oppervlakte';
				attribute<nrAansl> NrAsl            (BO) := ='WarmteVraag/' + BO/BCname + '/nrAansluitingen';
				attribute<kW>      P_id             (BO) := ='WarmteVraag/' + BO/BCname + '/AansluitCapaciteit';
				attribute<kW>      P_sec            (BO) := ='WarmteVraag/' + BO/BCname + '/P_sec';
				attribute<Bool>    isGroot          (BO) := P_id > afkapgrens_klein;
				
				// ========== investeringskosten wijkdistributie
				attribute<pand_asl>pand_aandeel	    (BO) :=	const(1.0[pand_asl], BO);
				attribute<m_pand>  L_aansl		    (BO) :=	/Invoer/RuimtelijkeData/buislengte/per_buurt/L_aansl_pand[BO/Planregio_rel];
	            container		   LeidingKosten		 :=	BuisKosten(BO, P_id * 0.001[ MW / kW]);
	            attribute<Eur>     Ki_wd_aansl      (BO) :=	isGroot ? 0.0[eur] : L_aansl * Leidingkosten/K_m * NrAsl * pand_aandeel;
				attribute<Eur>	   Ki_wd_OS		    (BO) := isGroot ? 0.0[eur] : P_id * Kostenkentallen/K_OS;
				
				// ========== investeringskosten inpandige distributie
				attribute<Eur>     Ki_id_dist       (BO) := isGroot ? 0.0[eur] : (Kostenkentallen/Ki_id_grondgeb + Kostenkentallen/Ki_afleverset) * NrAsl;
				attribute<Eur>	   Ki_id_OS		    (BO) := isGroot ? P_id * Kostenkentallen/K_OS : 0.0[eur];
			    attribute<Eur>	   Ki_id_Wmtr       (BO) := isGroot ? Kostenkentallen/K_Wmtr_vast * NrAsl + Kostenkentallen/K_Wmtr_var * P_id : 0.0[eur];
				
			    // ========== jaarlijkse kosten voor onderhoud en beheer
			    attribute<Eur_yr>  Kj_wd_OS_oh   	(BO) := Kengetallen/Onderhoud/R_OS_onderhoud   * Ki_wd_OS;
				attribute<Eur_yr>  Kj_id_OS_oh   	(BO) := Kengetallen/Onderhoud/R_OS_onderhoud   * Ki_id_OS;
			    attribute<Eur_yr>  Kj_id_dist_oh    (BO) := Kengetallen/Onderhoud/R_Sec_onderhoud  * Ki_id_dist;
			    attribute<Eur_yr>  Kj_id_Wmtr_oh    (BO) := Kengetallen/Onderhoud/R_Inst_Onderhoud * Ki_id_Wmtr;
			    
			    // ========== sommering berekeningen op objectniveau naar planregioniveau
			    container PR 
				{
					attribute<Eur>     Ki_id     (PlanRegio) := sum(Ki_id_dist + Ki_id_OS + Ki_id_Wmtr, BO/Planregio_rel);
					attribute<Eur>     Ki_wd     (PlanRegio) := sum(Ki_wd_OS, BO/Planregio_rel);
					attribute<Eur_yr>  Kj_id_adm (PlanRegio) := sum(Kengetallen/Onderhoud/R_Inst_Admin * Ki_id_Wmtr, BO/Planregio_rel);
					attribute<Eur_yr>  Kj_id_oh  (PlanRegio) := sum(Kj_id_OS_oh + Kj_id_dist_oh + Kj_id_Wmtr_oh, BO/Planregio_rel);
					attribute<Eur_yr>  Kj_wd_oh  (PlanRegio) := sum(Kj_wd_OS_oh, BO/Planregio_rel);
					attribute<kW>      P_id      (PlanRegio) := sum(../P_id, BO/Planregio_rel);
					attribute<kW>      P_sec     (PlanRegio) := sum(../P_sec, BO/Planregio_rel);
				}
			}
			// ========== vermogensvraag en inpandige kosten utiliteit per planregio
			
			attribute<kW>		P_sec     (PlanRegio) := bu/PR/P_sec     + nu/PR/P_sec;					
			attribute<Eur>		Ki_id     (PlanRegio) := bu/PR/Ki_id     + nu/PR/Ki_id;
			attribute<Eur>		Ki_wd     (PlanRegio) := bu/PR/Ki_wd     + nu/PR/Ki_wd;
			attribute<Eur_yr>	Kj_id_oh  (PlanRegio) := bu/PR/Kj_id_oh  + nu/PR/Kj_id_oh;
			attribute<Eur_yr>	Kj_id_adm (PlanRegio) := bu/PR/Kj_id_adm + nu/PR/Kj_id_adm;
			attribute<Eur_yr>   Kj_wd_oh  (PlanRegio) := bu/PR/Kj_wd_oh  + nu/PR/Kj_wd_oh;	
		}

		container GlasTuinbouw
		{
			container bt := GLTBdata(WarmteVraag/BestaandeGlTb/BO); //berekeningen op objectniveau bestaande glastuinbouw
			container nt := GLTBdata(WarmteVraag/NieuwbouwGLTB/BO); //berekeningen op objectniveau nieuwbouw glastuinbouw
			
			template GLTBdata
			{
				unit<UInt32>       BO;
				
				// ========== oppervlak, aansluitingen en vermogensvraag
				attribute<m2>      Areaal           (BO) := ='WarmteVraag/' + BO/BCname + '/oppervlakte';
				attribute<nrAansl> NrAsl            (BO) := ='WarmteVraag/' + BO/BCname + '/nrAansluitingen';
				attribute<kW>      P_id             (BO) := ='WarmteVraag/' + BO/BCname + '/AansluitCapaciteit';
				
				// ========== bepaling lengte en kosten per meter van inpandige distributieleidingen
				attribute<Eur>     K_dienstleiding  (BO) := ='Areaal* Kostenkentallen/K_dienstleiding_m2[Bebouwing/' + BO/BCname + '/BebouwingsType]';
				attribute<float64> n_Zijleiding     (BO) := float64(roundUp(Sqrt(2.0) * Sqrt(Areaal) / Kostenkentallen/GroenteKasLengteFactor)) * 2.0;
				attribute<MW>      P_Zijleiding     (BO) := P_id / n_Zijleiding / 1000[kW / MW];
				attribute<m>       L_Zijleiding     (BO) := (BO/IsNieuwbouw ? 1.3 : 1.7) * (Areaal / Kostenkentallen/GroenteKasLengteFactor + Sqrt(2.0) * Sqrt(Areaal));
				container          ZijleidingKosten       := BuisKosten(BO, P_Zijleiding);		
				
				// ========== investeringskosten inpandige distributie
				attribute<Eur>     Ki_id_dist       (BO) := L_Zijleiding * ZijleidingKosten/K_m + K_dienstleiding;
				attribute<Eur>	   Ki_id_OS		    (BO) := P_id * Kostenkentallen/K_OS;
			    attribute<Eur>	   Ki_id_Wmtr       (BO) := Kostenkentallen/K_Wmtr_vast * NrAsl + Kostenkentallen/K_Wmtr_var * P_id;
			    
			    // ========== jaarlijkse kosten voor onderhoud en beheer
			    attribute<Eur_yr>  Kj_id_CO2Inst    (BO) := Areaal / 10000[m2 / ha] * (Kostenkentallen/Ki_huur_CO2Inst + Kostenkentallen/V_CO2 * Kostenkentallen/K_CO2);
				attribute<Eur_yr>  Ki_id_OS_oh   	(BO) := Kengetallen/Onderhoud/R_OS_onderhoud   * Ki_id_OS;
			    attribute<Eur_yr>  Kj_id_dist_oh    (BO) := Kengetallen/Onderhoud/R_Sec_onderhoud  * Ki_id_dist;
			    attribute<Eur_yr>  Kj_id_Inst_oh    (BO) := Kengetallen/Onderhoud/R_Inst_Onderhoud * ((Kj_id_CO2Inst * 1[yr]) + Ki_id_Wmtr);
			    
			    // ========== sommering berekeningen op objectniveau naar planregioniveau
			    container PR 
				{
					attribute<Eur>     Ki_id     (PlanRegio) := sum(Ki_id_dist + Ki_id_OS + Ki_id_Wmtr, BO/Planregio_rel);
					attribute<Eur_yr>  Kj_id_adm (PlanRegio) := sum(Kengetallen/Onderhoud/R_Inst_Admin * ((Kj_id_CO2Inst * 1[yr]) + Ki_id_Wmtr), BO/Planregio_rel);
					attribute<Eur_yr>  Kj_id_oh  (PlanRegio) := sum(Kj_id_CO2Inst + Ki_id_OS_oh + Kj_id_dist_oh + Kj_id_Inst_oh, BO/Planregio_rel);
					attribute<kW>      P_id      (PlanRegio) := sum(../P_id, BO/Planregio_rel);
				}
			}
			// ========== vermogensvraag en inpandige kosten glastuinbouw per planregio
			attribute<kW>		P_sec     (PlanRegio) := Vermogens/GTF_t_RVmt * (bt/PR/P_id + nt/PR/P_id);					
			attribute<Eur>		Ki_id     (PlanRegio) := bt/PR/Ki_id     + nt/PR/Ki_id;
			attribute<Eur_yr>	Kj_id_oh  (PlanRegio) := bt/PR/Kj_id_oh  + nt/PR/Kj_id_oh;
			attribute<Eur_yr>	Kj_id_adm (PlanRegio) := bt/PR/Kj_id_adm + nt/PR/Kj_id_adm;
		}

		container SecundairNet
		{
			unit<uint8>			Prijzen_elec					:= Prijzen/Elektriciteit/Staffel;
			parameter<classifications/gebruiksgrootteklasse>	wd_gebruiksgrootteklasse_rel := last(ID(Prijzen_elec)), Descr = "elektriciteitstarief voor wijkdistributeur";
			container			LeidingKosten					:=	BuisKosten(PlanRegio, P_sec * 0.001[ MW / kW]);
			
			attribute<KW>		P_sec          (PlanRegio)	:=	(Wonen/bw/P_sec + Wonen/nw/P_sec + Utiliteit/P_sec + GlasTuinbouw/P_sec) / (1d - Efficiencies/Vermogenverlies_MT);
			attribute<m>		L_hoofdleiding (PlanRegio)	:=	0.25 * /Invoer/RuimtelijkeData/buislengte/per_buurt/L_dist, Descr = "aantal meter distributieleidingen (hoofd)", Source = "aanlevering GreenVis februari 2020";
			attribute<Eur>		K_hoofdleiding (PlanRegio)	:=	L_hoofdleiding * Leidingkosten/K_m;
			attribute<m>		L_zijleiding   (PlanRegio)	:=	0.75 * /Invoer/RuimtelijkeData/buislengte/per_buurt/L_dist, Descr = "aantal meter distributieleidingen (zij)", Source = "aanlevering GreenVis februari 2020";
			attribute<Eur>		K_zijleiding   (PlanRegio)	:=	L_zijleiding * Leidingkosten/K_m;
			attribute<Eur>		K_WOS          (PlanRegio)	:=	Efficiencies/HulpketelVermogensaandeel * P_sec * Kostenkentallen/K_WOS;
			attribute<Eur>		Ki_Totaal      (PlanRegio)	:=	K_hoofdleiding + K_zijleiding + K_WOS;
			attribute<m>		L_transport    (Planregio)	:=	L_hoofdleiding + L_zijleiding;
			attribute<Eur_yr>	Kj_Onderhoud   (PlanRegio)	:=	(K_hoofdleiding + K_zijleiding) * Kengetallen/Onderhoud/R_Sec_onderhoud + K_WOS * Kengetallen/Onderhoud/R_WOS_onderhoud;
			attribute<Eur_yr>	Kj_Admin       (PlanRegio)	:=	const(0.0[Eur_yr],PlanRegio);
			
			attribute<GJ_yr>	V_wd_elek      (PlanRegio)	:=	V_warmte * Efficiencies/Pompenergie_Wnet,								Descr = "elektriciteitsverbruik distributiepompen";	
			attribute<Eur_yr>	Kj_wd_elek     (PlanRegio)	:=	V_wd_elek * Prijzen_elec/KGJ_eindgebruik_excl	[wd_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten collectief eindgebruiker";
			attribute<Eur_yr>	Km_wd_elek     (PlanRegio)	:=	V_wd_elek * Prijzen_elec/KGJ_maatschappelijk	[wd_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten collectief maatschappelijk";
			attribute<Eur_yr>	Kj_wd_elek_EH  (PlanRegio)	:=	V_wd_elek * Prijzen_elec/KGJ_EnergieHeffing		[wd_gebruiksgrootteklasse_rel],	Descr = "energieheffing over elektriciteitsverbruik pompenergie";
			attribute<Eur_yr>	Kj_wd_elek_CO2 (PlanRegio)	:=	V_wd_elek * Prijzen_elec/KGJ_CO2Heffing			[wd_gebruiksgrootteklasse_rel],	Descr = "co2heffing over elektriciteitsverbruik pompenergie";
		}
		container PrimairNet
		{
			container			LeidingKosten				:=	BuisKosten(PlanRegio, P_transport * 0.001[ MW / kW]);
			attribute<kw>		P_transport    (PlanRegio)	:=	SecundairNet/P_sec;
			attribute<kw>		P_ASW          (PlanRegio)	:=	SecundairNet/P_sec;												
		}
		attribute<GJ_yr>	V_WarmteVraag    (PlanRegio) := ='add('+AsItemList('sum(WarmteVraag/'+BCname+'/V_Warmte, WarmteVraag/'+BCname+'/BO/PlanRegio_rel)' )+')';
		attribute<GJ_yr>	V_Warmte         (PlanRegio) :=	V_WarmteVraag / (1d - Efficiencies/Leidingverlies_LT * Leercurves/Verl/Curve);
		attribute<GJ_yr>	V_LeidingVerlies (PlanRegio) :=	V_Warmte - V_WarmteVraag;
		attribute<GJ_yr>	V_WarmtePrimair  (PlanRegio) :=	V_Warmte;

		container Bijstook
		{
			attribute<GJ_yr>	V_Vraag       (PlanRegio) :=	V_Warmte,			Descr = "volumevraag naar warmte die jaarlijks wordt geleverd uit de hulpketel";
			attribute<GJ_yr>	V_Bijstook    (PlanRegio) :=	V_Vraag / Efficiencies/Efficiency_piekketel,				Descr = "volumevraag naar warmte uit hulpketel na correctie voor efficientiefactor", Source = "FO v7a F 35";
			attribute<GJ_yr>	V_Verlies     (PlanRegio) :=	V_Bijstook - V_Vraag,										Descr = "gedeelte van de benodigde energie in de hulpketel dat verloren gaat in de omzetting naar warmte";
			attribute<Eur_yr>	Kj_wd_gas     (PlanRegio) :=	Prijzen/AardGas/Bijstook/KGJ_eindgebruik_excl * V_Bijstook,	Descr = "eindgebruikerskosten voor inkoop aardgas voor bijstook ik hulpketels";
			attribute<Eur_yr>	Km_wd_gas     (PlanRegio) :=	Prijzen/AardGas/Bijstook/KGJ_maatschappelijk  * V_Bijstook,	Descr = "maatschappelijke kosten voor inkoop aardgas voor bijstook in hulpketels";
			attribute<Eur_yr>	Kj_wd_gas_EH  (PlanRegio) :=	prijzen/Aardgas/Bijstook/KGJ_EnergieHeffing   * V_Bijstook,	Descr = "energiebelasting betaald over inkoop aardgas voor bijstook in hulpketels door eindgebruikers, als onderdeel van Kj_wd_gas";
			attribute<Eur_yr>	Kj_wd_gas_CO2 (PlanRegio) :=	prijzen/Aardgas/Bijstook/KGJ_CO2Heffing       * V_Bijstook,	Descr = "CO2 heffing betaald over inkoop aardgas voor bijstook in hulpketels door eindgebruikers, als onderdeel van Kj_wd_gas";
		}

		attribute<Eur>    Ki_id           (PlanRegio) := Wonen/Ki_id + Utiliteit/Ki_id + Glastuinbouw/Ki_id;
		attribute<Eur>    Ki_wd           (PlanRegio) := SecundairNet/Ki_Totaal + Wonen/Ki_wd + Utiliteit/Ki_wd;

		attribute<Eur_yr> Kj_id_oh        (PlanRegio) := Leercurves/OenM/Curve * (Wonen/Kj_id_oh + Utiliteit/Kj_id_oh + Glastuinbouw/Kj_id_oh);
		attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := Leercurves/OenM/Curve * (Wonen/Kj_wd_oh + Utiliteit/Kj_wd_oh + SecundairNet/Kj_Onderhoud);
		attribute<Eur_yr> Kj_Onderhoud    (PlanRegio) := Kj_wd_oh + Kj_id_oh;
		attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := SecundairNet/Kj_wd_elek;
		attribute<Eur_yr> Km_wd_elek      (PlanRegio) := SecundairNet/Km_wd_elek;
		attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := Bijstook/Kj_wd_gas;
		attribute<Eur_yr> Km_wd_gas       (PlanRegio) := Bijstook/Km_wd_gas;
		attribute<Eur_yr> Kj_lv_adm       (PlanRegio) := Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_lv_Admin * (opbrengsten/JaarlijksD/Oj_VastRecht * 1[yr]);
		attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := Leercurves/OenM/Curve * (Wonen/Kj_wd_adm + Utiliteit/Kj_id_adm + Glastuinbouw/Kj_id_adm + SecundairNet/Kj_Admin);

		attribute<nrAansl>nrAansl_d       (PlanRegio) := ='add('+AsItemList(replace('sum(Bebouwing/@BC@/nrAansluitingen * KanGebiedsOptie/@BC@, Bebouwing/@BC@/PlanRegio_rel)', '@BC@', BCname))+')';
		attribute<nrAansl>nrAansl_g       (PlanRegio) := ='add('+AsItemList(replace('sum(Bebouwing/@BC@/nrAansluitingen * KanGebiedsOptie/@BC@, Bebouwing/@BC@/PlanRegio_rel)', '@BC@', BCname))+')';
		attribute<nrAansl>nrAansl_e       (PlanRegio) := ='add('+AsItemList(replace('sum(Bebouwing/@BC@/nrAansluitingen, Bebouwing/@BC@/PlanRegio_rel)', '@BC@', BCname))+')';
		attribute<m>      L_distr_g       (PlanRegio) := nrAansl_g * Kengetallen/Constanten/L_di_gem;
		attribute<m>      L_distr_e       (PlanRegio) := nrAansl_e * Kengetallen/Constanten/L_di_gem;
		attribute<m>      L_infra_g       (PlanRegio) := L_distr_g + SecundairNet/L_transport;
		attribute<m>      L_infra_e       (PlanRegio) := L_distr_e + SecundairNet/L_transport;

		attribute<Eur_yr> Kj_Totaal       (PlanRegio) := Kj_Onderhoud + Kj_wd_gas + Kj_wd_elek + Kj_wd_adm + Kj_lv_adm + Kj_ge_hv, Source = "FO v7a F 2";
		attribute<Eur_yr> Km_Totaal       (PlanRegio) := Kj_Onderhoud + Km_wd_gas + Km_wd_elek + Kj_wd_adm + Kj_lv_adm ;
	}

	attribute<Eur   > Oi_Aansluitbijdrage (PlanRegio) := Opbrengsten/EenmaligD/Oi_Totaal;
	attribute<Eur_yr> Oji_wd_KL           (PlanRegio) := Oi_Aansluitbijdrage * NCW/wd28/StartDiscountFactor * NCW/wd28/AnnualisationFactor;
	attribute<Eur_yr> Kji_ge_KL           (PlanRegio) := Ki_ge_Bruto * ( NCW/bw28/StartDiscountFactor * NCW/bw28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kji_id_KL           (PlanRegio) := Ki_id_Bruto * ( NCW/id28/StartDiscountFactor * NCW/id28/AnnualisationFactor ) ;

	attribute<Eur>    Ki_ge_ov            (PlanRegio) := KostenD/Wonen/Ki_ge_ov;
	attribute<Eur>    Ki_ge_pm            (PlanRegio) := KostenD/Wonen/Ki_ge_pm;
	attribute<Eur_yr> Kj_ge_hv            (PlanRegio) := KostenD/Wonen/Kj_ge_hv;
	attribute<Eur>    Ki_ge_Bruto         (PlanRegio) := Ki_ge_ov + Ki_ge_pm;
	attribute<Eur   > Ki_id_Bruto         (PlanRegio) := KostenD/Ki_id;
	attribute<Eur   > Ki_wd_Bruto         (PlanRegio) := KostenD/Ki_wd;
	attribute<Eur_yr> Kji_ge_ov           (PlanRegio) := Ki_ge_ov    * ( NCW/bw28/StartDiscountFactor * NCW/bw28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kji_ge_pm           (PlanRegio) := Ki_ge_pm    * ( NCW/bw28/StartDiscountFactor * NCW/bw28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kji_id              (PlanRegio) := Ki_id_Bruto * ( NCW/id28/StartDiscountFactor * NCW/id28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kji_wd              (PlanRegio) := Ki_wd_Bruto * ( NCW/wd28/StartDiscountFactor * NCW/wd28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kmi_ge_ov           (PlanRegio) := Ki_ge_ov    * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kmi_ge_pm           (PlanRegio) := Ki_ge_pm    * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kmi_id              (PlanRegio) := Ki_id_Bruto * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	attribute<Eur_yr> Kmi_wd              (PlanRegio) := Ki_wd_Bruto * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	
	attribute<Eur>    Oi_ge_subsidie      (PlanRegio) := Ki_ge_ov * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_OngeriefsVergoeding + Ki_ge_pm * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_ProjectManagement;
	attribute<Eur>    Oi_id_subsidie      (PlanRegio) := Ki_id_Bruto * SpecifiekeInstellingen/Beleid/EIA/id;
	attribute<Eur>    Oi_wd_subsidie      (PlanRegio) := Ki_wd_Bruto * SpecifiekeInstellingen/Beleid/EIA/wd;
	attribute<Eur_yr> Oji_ge_EIA          (PlanRegio) := Oi_ge_subsidie * ( NCW/bw28/StartDiscountFactor * NCW/bw28/AnnualisationFactor ) ;
	attribute<Eur_yr> Oji_id_EIA          (PlanRegio) := Oi_id_subsidie * ( NCW/id28/StartDiscountFactor * NCW/id28/AnnualisationFactor ) ;
	attribute<Eur_yr> Oji_wd_EIA          (PlanRegio) := Oi_wd_subsidie * ( NCW/wd28/StartDiscountFactor * NCW/wd28/AnnualisationFactor ) ;
	attribute<Eur_yr> Omi_ge_EIA          (PlanRegio) := Oi_ge_subsidie * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	attribute<Eur_yr> Omi_id_EIA          (PlanRegio) := Oi_id_subsidie * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	attribute<Eur_yr> Omi_wd_EIA          (PlanRegio) := Oi_wd_subsidie * ( NCW/mr28/StartDiscountFactor * NCW/mr28/AnnualisationFactor ) ;
	
	attribute<Eur_yr> Kji_ge_base         (PlanRegio) := (Kji_ge_ov + Kji_ge_pm - Oji_ge_EIA) + Kj_ge_hv;
	attribute<Eur_yr> Kji_id_base         (PlanRegio) := (Kji_id - Oji_id_EIA) + KostenD/Kj_id_oh;
	attribute<Eur_yr> Kji_wd_base         (PlanRegio) := (Kji_wd - Oji_wd_EIA) + KostenD/Kj_wd_oh + KostenD/Kj_wd_adm;
	attribute<Eur_yr> Kji_ge_Netto        (PlanRegio) := Kji_ge_base;
	attribute<Eur_yr> Kji_id_Netto        (PlanRegio) := Kji_id_base;
	attribute<Eur_yr> Kji_wd_Netto        (PlanRegio) := Kji_wd_base  + KostenD/Kj_wd_gas + KostenD/Kj_wd_elek - Oji_wd_KL;
	attribute<Eur_yr> Kji_Netto           (PlanRegio) := Kji_ge_Netto + Kji_id_Netto + Kji_wd_Netto + KostenD/Kj_lv_adm;

	attribute<Eur_yr> Oji_wd              (PlanRegio) := Oji_wd_KL + Opbrengsten/JaarlijksD/Oj_Totaal;
	attribute<Eur_yr> Oji_NettoD          (PlanRegio) := Opbrengsten/JaarlijksD/Oj_Totaal - Kji_Netto;
	attribute<bool>   PartialRendabel     (PlanRegio) := Oji_NettoD > 0[Eur_yr];
	attribute<Eur_yr> Km_totaalD          (PlanRegio) := KostenD/Km_Totaal;
	
	attribute<MW>     CapaciteitsVraagD   (PlanRegio) := KostenD/PrimairNet/P_transport * 0.001 [mw / kw];
	attribute<GJ_yr>  V_WarmtePrimairD    (PlanRegio) := KostenD/V_WarmtePrimair;
	attribute<KG_yr>  CO2_BijstookD       (PlanRegio) := Prijzen/AardGas/CO2_GJ * KostenD/Bijstook/V_Bijstook;
}
