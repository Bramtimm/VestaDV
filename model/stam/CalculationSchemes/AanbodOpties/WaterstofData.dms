//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WaterstofData
{
	// begin case parameters
	container BCdata;
	parameter<Classifications/GebiedsOptie> Optie;
	// end case parameters

	unit<uint32>		BebouwingsObject   :=	BCdata/BebouwingsObject;
	unit<uint32>		PlanRegio          :=	Invoer/SpecifiekeInstellingen/PlanRegio;
	parameter<bool>		IsHybride          :=	Optie == Classifications/GebiedsOptie/V/WaterstofWP;
	parameter<bool>		IsNieuwbouw        :=	BCdata/IsNieuwbouw;
	
	//DEELNAME
	attribute<bool>		KanGebiedsOptie    (BebouwingsObject) :=	BcData/warmtevraag/KanGebiedsOptie;
	attribute<bool>		LabelOK            (BebouwingsObject) :=	IsHybride ? BcData/results/CurrentLabel <= /Classifications/Schillabel/V/E : const(true, BebouwingsObject);
	attribute<bool>		WPgeschikt         (BebouwingsObject) :=	KanGebiedsOptie && LabelOK;
	attribute<nrAansl>	nrAansluitingen    (BebouwingsObject) :=	BcData/warmtevraag/deelnemers/nrAansluitingen;
	attribute<kw>		AansluitCapaciteit (BebouwingsObject) :=	BcData/warmtevraag/deelnemers/AansluitCapaciteit;
	attribute<m2>		oppervlakte        (BebouwingsObject) :=	BcData/warmtevraag/deelnemers/oppervlakte;
	
	//VRAAG
	attribute<GJ_yr>	V_RV               (BebouwingsObject) :=	BcData/warmtevraag/deelnemers/Verbruik_RV,	Descr = "vraag naar ruimteverwarming";
	attribute<GJ_yr>	V_APP              (BebouwingsObject) :=	BCdata/results/Functioneel/Vj_app,			Descr = "vraag naar elektriciteit voor apparatuur";
	attribute<GJ_yr>	V_TW               (BebouwingsObject) :=	BcData/warmtevraag/deelnemers/Verbruik_WW,	Descr = "vraag naar warm tapwater";
	
	//EFFICIENCY
	parameter<ratio>	Eff_H2_RV        := Kentallen/Eff_H2_RV;
	parameter<ratio>	Eff_H2_TW        := Kentallen/Eff_H2_TW;
	
	attribute<ratio>	SPF_elek_rv  (BebouwingsObject) := 
		BcData/results/CurrentLabel <= /Classifications/Schillabel/V/A ? Kentallen/SPF_WP_RV_A :
		BcData/results/CurrentLabel <= /Classifications/Schillabel/V/B ? Kentallen/SPF_WP_RV_B : Kentallen/SPF_WP_RV_C, Descr = "Efficientie van de individuele warmtepomp voor eigen ruimteverwarming";
		
	attribute<ratio>	Dekking_WP  (BebouwingsObject) := !IsHybride || !WPgeschikt ? 0.0 : (
		BcData/results/CurrentLabel <= /Classifications/Schillabel/V/A ? Kentallen/Dekking_WP_A :
		BcData/results/CurrentLabel <= /Classifications/Schillabel/V/B ? Kentallen/Dekking_WP_B : Kentallen/Dekking_WP_C), Descr = "Dekkingsgraad warmtepomp, afhankelijk van schillabel";
	
	//VERBRUIK
	attribute<GJ_yr>	V_H2_TW (BebouwingsObject) :=	float64(KanGebiedsOptie) * (V_TW / Eff_H2_TW);
	attribute<GJ_yr>	V_H2_RV (BebouwingsObject) :=	float64(KanGebiedsOptie) * (V_RV * (1.0 - Dekking_WP) / Eff_H2_RV);
	attribute<GJ_yr>	V_pomp  (BebouwingsObject) :=	float64(KanGebiedsOptie) * Kentallen/PompEnergie_cap * AansluitCapaciteit[gj_yr];
	attribute<GJ_yr>	V_elek  (BebouwingsObject) :=	float64(KanGebiedsOptie) * (V_RV * Dekking_WP / SPF_elek_rv) + (IsHybride && WPgeschikt ? 0.0[gj_yr] : V_pomp);
		
	//KOSTEN	
	attribute<classifications/gebruiksgrootteklasse> ind_gebruiksgrootteklasse_rel(BebouwingsObject) :=
		classify((V_Elek + V_APP), Prijzen_elec/ClassBreak);
	
	parameter<eur_aansl>Ki_hWP_asl  := BCdata/BC_kentallen/Ki_hWP_vast;
	parameter<eur_kw>	Ki_hWP_cap  := BCdata/BC_kentallen/Ki_hWP_var;

	attribute<Eur_yr>	Kj_elek_EH  (BebouwingsObject) :=	V_Elek * Prijzen_elec/KGJ_EnergieHeffing[ind_gebruiksgrootteklasse_rel];	
	attribute<Eur_yr>	Kj_elek_CO2 (BebouwingsObject) :=	V_Elek * Prijzen_elec/KGJ_CO2Heffing[ind_gebruiksgrootteklasse_rel];		
	attribute<Eur_yr>	Kj_elek	    (BebouwingsObject) :=	V_Elek * Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel];
	attribute<Eur_yr>	Km_elek     (BebouwingsObject) :=	V_Elek * Prijzen_elec/KGJ_maatschappelijk [ind_gebruiksgrootteklasse_rel];
	attribute<KW>		ASW_WP      (BebouwingsObject) :=	BCdata/RuimtelijkeVraagComponent/Aansluitcapaciteit/RV;
	attribute<eur>		Ki_hWP      (BebouwingsObject) :=	float64(WPgeschikt) * (Ki_hWP_asl * nrAansluitingen + Ki_hWP_cap * ASW_WP);
	
	parameter<eur_aansl>	K_Aansl_asl :=
		BCdata/BCname == 'BestaandeWoning' || BCdata/BCname == 'NieuwbouwWoning' ? Kentallen/K_aansl_w : 0.0[eur_aansl];
	parameter<eur_m2>		K_Aansl_opp :=
		BCdata/BCname == 'BestaandeWoning' || BCdata/BCname == 'NieuwbouwWoning' ? 0.0[eur_m2] : Kentallen/K_aansl_u;
	parameter<eur_aansl>	K_Ketel_asl :=
		BCdata/BCname == 'BestaandeWoning' || BCdata/BCname == 'NieuwbouwWoning' ? Kentallen/K_Ketel_w : 0.0[eur_aansl];
	parameter<eur_kw>		K_Ketel_cap :=
		BCdata/BCname == 'BestaandeWoning' || BCdata/BCname == 'NieuwbouwWoning' ? 0.0[eur_kw] : Kentallen/K_Ketel_u;
		
	attribute<eur>		Ki_ketel    (BebouwingsObject) :=	K_Ketel_asl * nrAansluitingen + K_Ketel_cap * AansluitCapaciteit;
	attribute<eur>		Ki_aansl    (BebouwingsObject) :=	K_Aansl_asl * nrAansluitingen + K_Aansl_opp * oppervlakte;
	
	container PerPlanRegio
	{
			attribute<nrAansl>	Aansluitingen (PlanRegio) := sum(nrAansluitingen, BCdata/PlanRegio_rel);
			
			//VERBRUIK
			attribute<GJ_yr>	V_Warmte        (PlanRegio) :=	sum((V_RV + V_TW), BCdata/PlanRegio_rel);
			attribute<GJ_yr>	V_Waterstof     (PlanRegio) :=	sum((V_H2_TW + V_H2_RV), BCdata/PlanRegio_rel);
			attribute<GJ_yr>	V_id_elek       (PlanRegio) :=	sum(V_elek, BCdata/PlanRegio_rel);
			
			//KOSTEN
			attribute<eur>		Ki_id_WP        (PlanRegio) :=	sum(Ki_hWP, BCdata/PlanRegio_rel);
			attribute<Eur_yr>	Kj_id_elek      (PlanRegio) :=	sum(Kj_elek, BCdata/PlanRegio_rel);
			attribute<Eur_yr>	Km_id_elek      (PlanRegio) :=	sum(Km_elek, BCdata/PlanRegio_rel);
			attribute<Eur_yr>	Kj_id_elek_EH   (PlanRegio) :=	sum(Kj_elek_EH, BCdata/PlanRegio_rel);
			attribute<Eur_yr>	Kj_id_elek_CO2  (PlanRegio) :=	sum(Kj_elek_CO2, BCdata/PlanRegio_rel);
			
			attribute<Eur>		Ki_ge_pm        (PlanRegio) :=	Aansluitingen * Kentallen/K_pm * float64(!IsNieuwbouw);
			attribute<Eur>		Ki_ge_ov        (PlanRegio) :=	Aansluitingen * Kentallen/K_ov * float64(!IsNieuwbouw);
			
			attribute<Eur>		Ki_Aansl         (PlanRegio) :=	sum(../Ki_Aansl, BCdata/PlanRegio_rel);
			attribute<Eur>		Ki_Ketel         (PlanRegio) :=	sum(../Ki_Ketel, BCdata/PlanRegio_rel);
	}
}