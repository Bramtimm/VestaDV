//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WKOUtilData
{
	// begin case parameters
	unit<uint32> BO;
	container WarmteVraagComponent;
	parameter<bool> IsNieuwbouw;
	parameter<bool> KoudeBeschikbaar;
	// end case parameters

	parameter<Eur_GJ> WarmtePrijs :=BO/WarmteWet/WarmtePrijs;

	attribute<GJ_yr> Lq4bis    (BO) := KoudeBeschikbaar ? WarmteVraagComponent/V_K : const(0[GJ_Yr], BO), Descr = "metervraag naar koude indien koude geleverd wordt";
	attribute<m2>    Lu        (BO) := WarmteVraagComponent/Oppervlakte;

	attribute<GJ_yr> V_RV      (BO) := WarmteVraagComponent/V_RV;
	attribute<GJ_yr> V_APP     (BO) := BO/Functioneel/V_App,	Descr = "vraag naar elektriciteit voor apparatuur";
	attribute<GJ_yr> V_TW      (BO) := WarmteVraagComponent/V_TW;
	attribute<GJ_yr> V_Warmte  (BO) := V_RV + V_TW;
	attribute<GJ_yr> V_Koude   (BO) := Lq4bis;
	
	attribute<float64>    SPF_ind_rv  (BO) := 
		BO/SchilLabel_rel <= /Classifications/Schillabel/V/A ? Efficiency/SPF_ind_A_rv :
		BO/SchilLabel_rel <= /Classifications/Schillabel/V/B ? Efficiency/SPF_ind_B_rv :
		BO/SchilLabel_rel <= /Classifications/Schillabel/V/E ? Efficiency/SPF_ind_CE_rv : 1.0, Descr = "Efficientie van de individuele warmtepomp voor eigen ruimteverwarming";
		
	attribute<float64>    AEO_ind_rv  (BO) := 
		BO/schilLabel_rel <= /Classifications/Schillabel/V/A ? Efficiency/AEO_ind_A_rv :
		BO/schilLabel_rel <= /Classifications/Schillabel/V/B ? Efficiency/AEO_ind_B_rv :
		BO/schilLabel_rel <= /Classifications/Schillabel/V/E ? Efficiency/AEO_ind_CE_rv : 1.0, Descr = "Aandeel eigen opwekking, afhankelijk van schillabel";
	
	//volumevraag gebouwzijde
	attribute<GJ_yr>	V_id_RV			(BO) := V_RV * AEO_ind_rv,						Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_id_TW			(BO) := V_TW * Efficiency/AEO_ind_tw,			Descr = "Aandeel van de eigen volumevraag naar warm tapwater dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_id_Koude		(BO) := V_Koude * Efficiency/AEO_ind_K,			Descr = "Aandeel van de eigen volumevraag naar koude dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_RV_sec		(BO) := V_RV - V_id_RV,							Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming geleverd uit het secundaire net";
	attribute<GJ_yr>	V_TW_sec		(BO) := V_TW - V_id_TW,							Descr = "Aandeel van de eigen volumevraag naar warm tapwater geleverd uit het secundaire net";
	
	//volumevraag jaarlijks aan het secundaire net (inclusief verrekening leidingverlies)
	attribute<GJ_yr>	V_Warmte_sec	(BO) := V_RV_sec + V_TW_sec,                             Descr = "Aandeel van de eigen volumevraag naar warmte uit het secundaire net";
	attribute<GJ_yr>	V_Koude_sec		(BO) := V_Koude - V_id_Koude,                            Descr = "Aandeel van de eigen volumevraag naar koude uit het secundaire net";
	attribute<GJ_yr>	V_Wsec_netto	(BO) := V_Warmte_sec / (1d - Efficiencies/Leidingverlies_LT * Leercurves/Verl/Curve), Descr ="Warmte te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr>	V_Ksec_netto	(BO) := V_Koude_sec  / (1d - Efficiencies/Leidingverlies_LT * Leercurves/Verl/Curve), Descr ="Koude te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr>	V_wd_verlies	(BO) := (V_warmte_sec - V_Wsec_netto) + (V_koude_sec - V_Ksec_netto);
	
	//Gelijktijdige capaciteitsvraag aan het secundaire net
	parameter<ratio>	GTFrv          := T_sec <= 55[Celsius] ? Vermogens/GTF_w_RVlt     : Vermogens/GTF_w_RVmt;
	parameter<kw_m2>	ASWrv_u_opp    := T_sec <= 55[Celsius] ? Vermogens/ASW_RVlt_u_opp : Vermogens/ASW_RVmt_u_opp;
	parameter<kw_asl>	ASWrv_u_asl    := T_sec <= 55[Celsius] ? Vermogens/ASW_RVlt_u_asl : Vermogens/ASW_RVmt_u_asl;

	attribute<kW>		P_TW_sec		(BO) := Vermogens/GTF_u_TW   * (Vermogens/ASW_TWlt_u_opp   * Lu + Vermogens/ASW_TWlt_u_asl   * 1.0[nrAansl]);
	attribute<kW>		P_RV_sec		(BO) := GTFrv * (ASWrv_u_opp * Lu + ASWrv_u_asl * 1.0[nrAansl]);
	attribute<kW>		P_Warmte_sec	(BO) := P_TW_sec + P_RV_sec;
	attribute<kW>		P_Koude_sec		(BO) := KoudeBeschikbaar ? Vermogens/GTF_u_k * (Vermogens/ASW_k_u_opp * Lu + Vermogens/ASW_k_u_asl * 1.0[nrAansl]) : 0[kW];
	attribute<kW>		P_Wsec_netto	(BO) := P_Warmte_sec / (1d - Efficiencies/Vermogenverlies_LT),	Descr ="Vermogen warmte te realiseren in het secundaire net met verrekening leidingverlies op vermogen";
	attribute<kW>		P_Ksec_netto	(BO) := P_Koude_sec / (1d - Efficiencies/Vermogenverlies_LT),		Descr ="Vermogen koude te realiseren in het secundaire net met verrekening leidingverlies op vermogen";
	attribute<kW>		P_sec			(BO) := max_elem(P_Wsec_netto, P_Ksec_netto),	Descr = "capaciteitsvraag aan secundair net";

	//elektriciteitsverbruik individueel (toegekend aan inpandig distributeur)
	attribute<GJ_yr>	V_id_RV_Elek	(BO) := V_id_RV / SPF_ind_rv,					Descr = "Elektriciteitsverbruik voor eigen opwekking tbv ruimteverwarming";
	attribute<GJ_yr>	V_id_TW_Elek	(BO) := V_id_TW / Efficiency/SPF_ind_tw,			Descr = "Elektriciteitsverbruik voor eigen opwekking tbv warm tapwater";
	attribute<GJ_yr>	V_id_W_Elek		(BO) := V_id_RV_Elek + V_id_TW_Elek,				Descr = "Elektriciteitsverbruik voor eigen opwekking tbv zowel ruimteverwarming als warm tapwater";
	attribute<GJ_yr>	V_id_K_elek		(BO) := V_id_Koude / Efficiency/SPF_ind_k,		Descr = "Elektriciteitsverbruik voor eigen opwekking tbv koude";
	attribute<GJ_yr>	V_id_Elek		(BO) := V_id_W_Elek + V_id_K_elek,				Descr = "Totaal elektriciteitsverbruik door afnemer";

	//elektriciteitsverbruik collectief (toegekend aan wijkdistributeur
	attribute<GJ_yr>	V_wd_W_Elek		(BO) := V_Wsec_netto / Efficiency/SPF_coll_w,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor warmteproductie";
	attribute<GJ_yr>	V_wd_K_Elek		(BO) := V_Ksec_netto / Efficiency/SPF_coll_k,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor koudeproductie";
	attribute<GJ_yr>	V_wd_D_Elek		(BO) := (V_Wsec_netto + V_Ksec_netto) * Efficiencies/Pompenergie_Wnet,		Descr = "Totaal elektriciteitsverbruik voor pompenergie distributienet";
	attribute<GJ_yr>	V_wd_Elek		(BO) := V_wd_W_Elek + V_wd_K_Elek + V_wd_D_Elek,	Descr = "Totaal elektriciteitsverbruik door wijkdistributeur";

	//extra berekeningen voor WKO
	attribute<GJ_yr> V_Warmte_ow        (BO) := V_Wsec_netto - V_wd_W_Elek; // warmte geleverd aan centrale eWP door opwekker tbv warmtelevering aan woning
	attribute<GJ_yr> V_Koude_ow         (BO) := V_Ksec_netto  + V_wd_K_Elek; // warmte onttrokken aan cenrale eWP door opwekker tbv koude levering aan woning
	attribute<GJ_yr> V_WKO              (BO) := V_Warmte_ow - V_Koude_ow; // netto aan de bodem of andere leverancier onttrokken warmte.

	attribute<classifications/gebruiksgrootteklasse> ind_gebruiksgrootteklasse_rel(BO) := classify((V_id_Elek + V_APP), Prijzen_elec/ClassBreak);
	parameter<classifications/gebruiksgrootteklasse> ow_gebruiksgrootteklasse_rel := last(ID(Prijzen_elec));

	container Kosten
	{
		//investeringskosten inpandige systemen
		attribute<Eur_m2>	K_wvu				(BO) :=
			Lu > 7500[m2] 
				?  Kostenkentallen/Ki_id_ugroot[Eur_m2] 
				: Lu < 100[m2]  
					? Kostenkentallen/Ki_id_uklein[Eur_m2] 
					: Kostenkentallen/Ki_id_uklein[Eur_m2] * (7500[m2] - Lu) / (7500[m2] - 100[m2]),				Descr = "kosten inpandige warmtevoorziening utiliteit(warmte en koude) Notitie issue 442";
		
		attribute<Eur>		Ki_eWP				(BO)	:=	max_elem(P_Warmte_sec, P_Koude_sec) * Kostenkentallen/Ki_eWP_util;
		attribute<Eur>		Ki_id_EigenOpwek	(BO)	:=	
			(BO/schilLabel_rel <= Classifications/Schillabel/V/A ) ? ((T_sec <= 55[Celsius]) ? Kostenkentallen/Ki_Booster : 0.0[eur]) :
			(BO/schilLabel_rel <= Classifications/Schillabel/V/B ) ? ((T_sec <= 35[Celsius]) ? Ki_eWP : (T_sec <= 55[Celsius]) ? Kostenkentallen/Ki_booster : 0.0[eur]) :
			(BO/schilLabel_rel <= Classifications/Schillabel/V/E ) ? ((T_sec <= 55[Celsius]) ? Ki_eWP : 0.0[eur]) 
			: 1000000.0[eur], Descr = "investeringskosten voor eigen opwekkingsinstallatie, afhankelijk van temperatuurniveau en schillabel";
			
		attribute<Eur_aansl>K_LTAS_asl			(BO)	:= const(BO/Bckentallen/Ki_LTAS_l_asl, BO);
		attribute<Eur_m2>	K_LTAS_opp			(BO)	:= const(BO/Bckentallen/Ki_LTAS_opp, BO);
		attribute<Eur>		Ki_LTAS				(BO)	:= K_LTAS_opp * WarmteVraagComponent/oppervlakte + K_LTAS_asl * WarmteVraagComponent/nrAansluitingen;
		attribute<Eur>		Ki_id_LTAS			(BO)	:= (T_sec <= 55[Celsius]) ? Ki_LTAS : 0 [Eur],	Descr = "investeringskosten laag-temperatuur afgiftesysteem, e.g. LT-radiatoren of vloerverwarming";			
		attribute<Eur>		Ki_id				(BO)	:= (K_wvu * Lu) + Ki_id_EigenOpwek +Ki_id_LTAS,		Descr = "Investeringskosten systeem bij utiliteit, F 64";
		
		//geen gebouweigenaar-kosten bij utiliteit
		attribute<Eur>		Ki_ge_pm			(BO)	:= const(0[Eur], BO),		Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>		Ki_ge_ov			(BO)	:= const(0[Eur], BO),		Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>		Oi_ge_EIA			(BO)	:= const(0[Eur], BO),		Descr = "geen gebouweigenaar-kosten bij utiliteit";
		attribute<Eur>		Ki_ge				(BO)	:= const(0[Eur], BO),		Descr = "geen gebouweigenaar-kosten bij utiliteit";

		//investeringskosten wijkdistributie gebouwgerelateerd, zonder distributieleidingen
		attribute<Eur>		Ki_wd_OS			(BO)	:= Kostenkentallen/Ki_OnderStation [Eur_KW] * P_sec [KW], Descr = "Investeringskosten onderstations";
		attribute<m_pand>	L_aansl				(BO)	:= /Invoer/RuimtelijkeData/buislengte/per_buurt/L_aansl_pand[BO/Planregio_rel];
		container			LeidingKosten :=	BuisKostenBC(BO, P_sec * 0.001[ MW / kW]);
		attribute<Eur_m>	K_aansl_m			(BO)	:=	Leidingkosten/K_m;	

		attribute<Eur>		Ki_wd_aansluiting	(BO)	:= L_aansl * K_aansl_m * 1[nrPanden], Descr = "Investeringskosten aansluitleidingen";
		attribute<Eur>		Ki_wd				(BO)	:= Ki_wd_OS + Ki_wd_aansluiting,	  Descr = "Investeringskosten wijkdistributie exclusief distributieleidingen";

		//Jaarlijkse kosten elektriciteitsverbruik
		attribute<Eur_yr>	Kj_id_elek			(BO)	:= V_id_Elek * Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten individueel eindgebruiker",	Source = "FO v7a p 82 F 57";
		attribute<Eur_yr>	Km_id_elek			(BO)	:= V_id_Elek * Prijzen_elec/KGJ_maatschappelijk [ind_gebruiksgrootteklasse_rel],	Descr = "jaarlijkse elektriciteitskosten individueel maatschappelijk",	Source = "FO C6 Tabel 6 WKO";
		attribute<Eur_yr>	Kj_wd_elek			(BO)	:= V_wd_elek * Prijzen_elec/KGJ_eindgebruik_excl[ow_gebruiksgrootteklasse_rel],		Descr = "jaarlijkse elektriciteitskosten collectief eindgebruiker",		Source = "FO v7a p 82 F 57";
		attribute<Eur_yr>	Km_wd_elek			(BO)	:= V_wd_elek * Prijzen_elec/KGJ_maatschappelijk [ow_gebruiksgrootteklasse_rel],		Descr = "jaarlijkse elektriciteitskosten collectief maatschappelijk",	Source = "FO C6 Tabel 6 WKO";	
		attribute<Eur_yr>	Kj_elek				(BO)	:= Kj_id_elek + Kj_wd_elek,	Descr = "jaarlijkse kosten elektriciteitsverbruik eindgebruiker";
		attribute<Eur_yr>	Km_elek				(BO)	:= Km_id_elek + Km_wd_elek,	Descr = "jaarlijkse kosten elektriciteitsverbruik maatschappelijk";

		//energieheffing en co2prijs
		attribute<Eur_yr>	Kj_id_elek_EH		(BO)	:= V_id_Elek * Prijzen_elec/KGJ_EnergieHeffing[ind_gebruiksgrootteklasse_rel],		Descr = "energieheffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>	Kj_id_elek_CO2		(BO)	:= V_id_Elek * Prijzen_elec/KGJ_CO2Heffing[ind_gebruiksgrootteklasse_rel],			Descr = "co2heffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>	Kj_wd_elek_EH		(BO)	:= V_wd_elek * Prijzen_elec/KGJ_EnergieHeffing[ow_gebruiksgrootteklasse_rel],		Descr = "energieheffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>	Kj_wd_elek_CO2		(BO)	:= V_wd_elek * Prijzen_elec/KGJ_CO2Heffing[ow_gebruiksgrootteklasse_rel],			Descr = "co2heffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>	Kj_elek_EH			(BO)	:= Kj_id_elek_EH  + Kj_wd_elek_EH,						Descr = "totaal energieheffing over elektriciteitsverbruik";
		attribute<Eur_yr>	Kj_elek_CO2			(BO)	:= Kj_id_elek_CO2 + Kj_wd_elek_CO2,						Descr = "totaal co2heffing over elektriciteitsverbruik";		
	}
	
	container Opbrengsten
	{
		attribute<Eur_GJ>	KoudePrijs			(BO)	:= Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel] / SpecifiekeInstellingen/NMDA_Prijzen/KoudePrijs/Utiliteit, Descr = "Prijs per GJ koude met als referentie individuele elektrische koudeproductie";

		attribute<Eur_yr>	Oj_Warmte			(BO)	:= V_Warmte * WarmtePrijs,							Descr = "opbrengsten voor leverancier uit warmtelevering";
		attribute<Eur_yr>	Oj_Koude			(BO)	:= KoudePrijs * Lq4bis,								Descr = "opbrengsten voor leverancier uit koudelevering", Source = "F 71";

		attribute<Eur>		Oi_Aansl			(BO)  := WarmteVraagComponent/Oi_AansluitBijdrage,			Descr = "eenmalige opbrengsten uit initiele aansluitbijdrage", Source = "F 65 jo F 10";
		attribute<Eur_yr>	Oj_VastrechtMT      (BO)  :=	float64(T_sec >  55[Celsius]) * WarmteVraagComponent/Oj_VastrechtMT, Descr = "jaarlijkse opbrengsten uit vastrecht voor temperaturen 70 of hoger";
		attribute<Eur_yr>	Oj_VastrechtLT      (BO)  :=	float64(T_sec <= 55[Celsius]) * WarmteVraagComponent/Oj_VastrechtLT, Descr = "jaarlijkse opbrengsten uit vastrecht voor temperaturen onder 70";
		attribute<Eur_yr>	Oj_VastrechtK       (BO)  :=	float64(KoudeBeschikbaar)     * WarmteVraagComponent/Oj_VastrechtK,  Descr = "jaarlijkse opbrengsten uit vastrecht voor koudeaansluiting";
		attribute<Eur_yr>	Oj_Vastrecht        (BO)  :=	Oj_VastrechtMT + Oj_VastrechtLT + Oj_VastrechtK;
		attribute<Eur_yr>	Om_Comfort			(BO)	:= Oj_Koude,										Descr = "jaarlijkse maatschappelijke opbrengsten in de vorm van comfort door koudelevering, gewaardeerd als de opbrengsten uit koude";
		
	}
	container RetabiliteitWP
	{
		attribute<string> S1AofB (BO) := Invoer/RuimtelijkeData/leidraad_oktober_2019/buurt/S1_variant_keuze[BO/Planregio_rel];
		parameter<string> criterium := 'Criteria/Always';
		container VerbruiksOpties := BO/BebouwingsComponent_UpRef/VerbruiksOpties; // to be used in GebouwOptieT
		container LuchtWP := CalculationSchemes/tussenresultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/LeWPu, criterium);
		container BodemWP := CalculationSchemes/tussenresultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/BeWPu, criterium);
		container HRketel := CalculationSchemes/tussenresultaten/GebouwOptieT(BO, Classifications/GebouwOptie/V/HRu, criterium);
		attribute<Eur_yr> Kj_totaal_HR (BO) := HRketel/kosten_m;
		attribute<Eur_yr> Kj_totaal_WP (BO) := S1AofB == 's1a'? LuchtWP/kosten_m : BodemWP/kosten_m;
		attribute<Eur_yr> Delta_totaal (BO) := =Voorkeuren/Afweging/LTversusWP ? 'Kj_totaal_WP - Kj_totaal_HR' : '0.0[eur_yr]';
	}
}