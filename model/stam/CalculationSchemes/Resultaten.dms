//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template Resultaten
{
	// begin case parameters
	parameter<String> RekenstapName;
	// optional case parameter

	parameter<bool> isStap0 := RekenstapName == 'StartJaar';
	
	parameter<Classifications/RekenStap> Rekenstap_rel  := rlookup(RekenstapName, Classifications/rekenstap/name);
	parameter<String> RekenjaarName   := Classifications/rekenjaar/Name[Classifications/RekenStap/rekenjaar_rel          [Rekenstap_rel]];
	parameter<String> BouwjaarName    := Classifications/Zichtjaar/Name[Classifications/RekenStap/zichtjaar_rel_bebouwing[Rekenstap_rel]];
	parameter<String> KlimaatjaarName := Classifications/Zichtjaar/Name[Classifications/RekenStap/zichtjaar_rel_klimaat  [Rekenstap_rel]];

	parameter<Classifications/Zichtjaar> ZichtJaar_rel := rlookup(BouwjaarName, Classifications/zichtjaar/name);
	parameter<units/yr_uint16>           ZichtJaar_jaar:= Classifications/zichtjaar/jaar[Zichtjaar_rel];
	parameter<Classifications/RekenJaar> RekenJaar_rel := Classifications/Rekenstap/RekenJaar_rel[Rekenstap_rel];
	parameter<units/yr_uint16>           RekenJaar_jaar:= Classifications/Rekenstap/Jaar         [Rekenstap_rel];
	
	container TussenResultaten      := ='/TussenResultaten/' + RekenstapName		, isHidden = "true";
	unit<uint32> PlanRegio          := Invoer/SpecifiekeInstellingen/PlanRegio		, isHidden = "true";
	container Prijzen               := TussenResultaten/Prijzen						, isHidden = "true";
	container BebouwingsComponenten := TussenResultaten/BebouwingsComponenten		, isHidden = "true";
	container NCW                   := TussenResultaten/NCW							, isHidden = "true";
	container GebiedsAllocatie      := TussenResultaten/GebiedsAllocatie			, isHidden = "true";
	container StateVoorAllocatie    := TussenResultaten/StateVoorAllocatie			, isHidden = "true";
	container StateNaAllocatie      := TussenResultaten/StateNaAllocatie			, isHidden = "true";
	container Voorkeuren            := TussenResultaten/Voorkeuren					, isHidden = "true";
	container Allocatie             := TussenResultaten/AllocatieResultaten			, isHidden = "true";

	container Generate: isHidden = "false"
	{
		container impl
		{
			parameter<string> ExplicitSupplierList :=
					AsList('InputOutput/'+SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')
	//				+ (SpecifiekeInstellingen/Rapportage/Regio/Name != 'NL' ? ';InputOutput/Totaal/GenerateAll': '')
					+ GenerateSamenstelling/impl/ExplicitSupplierList
	//				+ ';' + 'AllocExports/GenerateThisToo' // comment-out if you want to skip all exports
				;
		}
		parameter<String> All := 'Ready', ExplicitSuppliers = "= impl/ExplicitSupplierList";
	}
	
	container GenerateSamenstelling: isHidden = "false"
	{
		container impl
		{
			parameter<string> ExplicitSupplierList :=
				AsList(
					replace(';' + AsList('MaatregelenRapportage/@BC/'+SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';'),'@BC',Classifications/BebouwingsComponent/Name)
					,';'
					);
		}
		parameter<String> All := 'Ready', ExplicitSuppliers = "= impl/ExplicitSupplierList";
	}
	
	container InputOutput :=
		for_each_ne(
			 SpecifiekeInstellingen/Rapportage/Regio/Name
		,	'Rapportage/Flow('
				'SpecifiekeInstellingen/Rapportage/Regio'
				','+Quote(SpecifiekeInstellingen/Rapportage/Regio/Name)+
				', value('+ string(id(SpecifiekeInstellingen/Rapportage/Regio)) + ',SpecifiekeInstellingen/Rapportage/Regio)'
			')'
		)
	{
		container Totaal := =(SpecifiekeInstellingen/Rapportage/Regio/Name != 'NL')
			? 'Rapportage/Flow(geography/RegioIndelingen/NL, ''NL'', value(0, geography/RegioIndelingen/NL) )'
			: '.';
	}
	
	
	
	container Eindgebruiker :=
			for_each_ne(Classifications/BebouwingsComponent/name,
				replace(
					'Rapportage/EindgebruikerComponent(
						 Allocatie/@BC@
						,NCW/@NC@15
						,NCW/@NC@30
						,NCW/@NC@50
						,sum(Allocatie/@BC@/AardGas/Vastrecht,              BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/AardGas/AansluitBijdrage,       BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/Elektriciteit/Vastrecht,        BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/Elektriciteit/AansluitBijdrage, BebouwingsComponenten/@BC@/PlanRegio_rel)
						,StateNaAllocatie/KostenBaten/PlanRegioKaarten/@BC@
					)'
				,	'@BC@', Classifications/BebouwingsComponent/name
				,	'@NC@', Classifications/BebouwingsComponent/NcwRefBase
				)
			)
		,	Source = "FO v7a C6", isHidden = "true";
	
	#include <Maatschappelijk.dms>
	#include <EnergyResults.dms>
	#include <MaatregelenRapportage.dms>
	#include <Netwerken.dms>
	#include <EnergieProducent.dms>
	
	container FolderInfo: isHidden = "True"
	{
		parameter<string> OutputFolder := '%localDataProjDir%/Results/' + Expand(., '%configName%') + '/'+ RekenstapName;
	}
}