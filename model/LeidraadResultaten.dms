//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container LeidraadResultaten: using = "Model"
{   
	container Exports
	{
		unit<uint32> PlanRegio           := Invoer/SpecifiekeInstellingen/PlanRegio;
		unit<uint32> BestaandeWoningen   := /Invoer/RuimtelijkeData/Bebouwing/Impl/BestaandeWoning;
		unit<uint32> BestaandeUtil       := /Invoer/RuimtelijkeData/Bebouwing/Impl/BestaandeUtil;
		unit<uint32> BebouwingsObject    := /Invoer/RuimtelijkeData/Bebouwing/Impl/BestaandeWoning;
		
		container FolderInfo
		{
			// parameter<string> RunName        := Expand(., '%configName%');
			// parameter<string> Last_Rekenstap := 'ZichtJaar';
			// parameter<string> Last_Rekenstap := last(/rekenstap/name);
			
			parameter<string> Last_Rekenstap := 'StartJaar';
			parameter<string> RunName        := 'StartJaar';
			
			parameter<string> RegioNaam_GooiEnVechtstreek    := 'GooiEnVechtstreek';
			parameter<string> RegioNaam_Hengelo              := 'Hengelo';
			parameter<string> OutputFolder                   := '%localDataProjDir%/Results/RES_NHZ/'+RunName;
			parameter<string> OutputFolder_GooiEnVechtstreek := '%localDataProjDir%/Results/'+RegioNaam_GooiEnVechtstreek+'/'+RunName;
			parameter<string> OutputFolder_Hengelo           := '%localDataProjDir%/Results/'+RegioNaam_Hengelo+'/'+RunName;
			
			parameter<string> StudieGebied_GooiEnVechtstreek := 'GM0402,GM1942,GM0376,GM0417,GM0406,GM0457,GM1696'; //Hsum, Gooise Meren,Blaricum,Laren,Weesp,Wijdemeren
			parameter<string> StudieGebied_Hengelo           := 'GM0164'; //Hengelo
			parameter<string> StudieGebied_Havenstad         := 'BU03636604,BU03636602,BU03636601,BU03637104,BU03631000,BU03631001,BU03633601,BU03631307,BU03631306,BU03633600,BU03631105';
		}	

		container ESDL
		{
			// parameter<string> Generate_PerPlanRegio := Generate_PerPlanRegio_GooiEnVechtstreek + Generate_PerPlanRegio_Hengelo;

			// parameter<string> Generate_PerPlanRegio_GooiEnVechtstreek := Export_PerPlanRegio_GooiEnVechtstreek/result;
			// parameter<string> Generate_PerPlanRegio_Hengelo           := Export_PerPlanRegio_Hengelo/result;
			parameter<string> Generate_PerPlanRegio_Havenstad           := export_PerPlanRegio_Havenstad/result;
			
			Unit<uint32>  PerPlanRegio := PlanRegio, using = "FolderInfo"
			{
				attribute<String>       BU_CODE                         (Planregio) := PlanRegio/BU_CODE;
				attribute<String>       I01_buurtcode                   (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I01_buurtcode';
				attribute<String>       I02_buurtnaam                   (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I02_buurtnaam';
				attribute<String>       I03_wijkcode                    (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I03_wijkcode';
				attribute<String>       I04_wijknaam                    (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I04_wijknaam';
				attribute<String>       I05_gemeentecode                (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I05_gemeentecode';
				attribute<String>       I06_gemeentenaam                (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I06_gemeentenaam';
				attribute<String>       I07_energieregionaam            (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I07_energieregionaam';
				attribute<String>       I08_provincienaam               (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I08_provincienaam';
				attribute<nrAsl>        I09_aantal_woningen	            (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I09_aantal_woningen';
				attribute<nrAsl>        I10_aantal_utiliteit            (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I10_aantal_utiliteit';
				attribute<nrWoning>     I11_woningequivalenten          (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I11_woningequivalenten'; //130m2 utiliteit = 1 WEQ
				attribute<string>       Uitgesloten                     (Planregio) := ='LeidraadResultaten/'+Last_Rekenstap+'/PlanregioResults/Statisch/table/I13_Uitgesloten';
			
				attribute<GJ_yr_WEQ>    H01_Vraag_Totaal                (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H01_Vraag_totaal, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H02_Vraag_Ruimteverwarming      (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H02_Vraag_RV, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H03_Vraag_Tapwater              (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H03_Vraag_TW, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H04_Vraag_Ventilatie            (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H04_Vraag_Vent, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H05_Vraag_Koude                 (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H05_Vraag_K, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H06_Vraag_Apparaten             (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H06_Vraag_App, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_ha>     H07_Vraag_perhectare            (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H07_Vraag_tot_ha, 0[GJ_yr_ha])';
				attribute<GJ_yr_WEQ>    H08_Input_Totaal                (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H08_Input_Totaal, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H09_Input_Aardgas               (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H09_Input_Aardgas, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H10_Input_Duurzaamgas           (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H10_Input_Duurzaamgas, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H11_Input_Elektriciteit         (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H11_Input_Elektriciteit, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H12_Input_MTwarmte              (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H12_Input_MTwarmtebronnen, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H13_Input_LTwarmte              (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H13_Input_LTwarmtebronnen, 0[GJ_yr_WEQ])';
				attribute<GJ_yr_WEQ>    H14_Saldo_Omgevingswarmte       (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H14_Saldo_Omgevingswarmte, 0[GJ_yr_WEQ])';
				attribute<ton_yr>       H15_CO2_uitstoot                (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H15_CO2_uitstoot, 0[ton_yr])';
				attribute<Eur_yr>       H16_Nat_meerkost                (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H16_Nat_meerkost, 0[Eur_yr])';
				attribute<Eur_ton>      H17_Nat_meerkost_CO2            (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H17_Nat_meerkost_CO2, 0[Eur_ton])';
				attribute<Eur_yr_WEQ>   H18_Nat_meerkost_WEQ            (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H18_Nat_meerkost_WEQ, 0[Eur_yr_WEQ])';
				attribute<Eur_m3>       H19_Waarde_hernieuwbaar_gas     (Planregio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Hoofdindicatoren/Table/H19_Waarde_hernieuwbaar_gas, 0[Eur_m3])';
				
				attribute<nrAsl>        A01_Aansl_aardgas               (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A01_Aansl_aardgas , 0[nrAsl])';
				attribute<nrAsl>        A02_Aansl_eWP                   (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A02_Aansl_eWP , 0[nrAsl])';
				attribute<nrAsl>        A03_Aansl_MT                    (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A03_Aansl_MT , 0[nrAsl])';
				attribute<nrAsl>        A04_Aansl_LT                    (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A04_Aansl_LT , 0[nrAsl])';
				attribute<nrAsl>        A05_Aansl_hWP_HG                (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A05_Aansl_hWP_HG , 0[nrAsl])';
				attribute<nrAsl>        A06_Aansl_HR_HG                 (PlanRegio) := ='MakeDefined(LeidraadResultaten/'+Last_Rekenstap+'/PlanRegioResults/Aansluitingen/Table/A06_Aansl_HR_HG , 0[nrAsl])';
				
				attribute<nrAsl>        A10_Aansl_LT_WKO                (PlanRegio) := Extra_aansluitingen/A10_Aansl_LT_WKO;
				attribute<nrAsl>        A11_Aansl_LT_LT15_30            (PlanRegio) := Extra_aansluitingen/A11_Aansl_LT_LT15_30;
				attribute<nrAsl>        A12_Aansl_LT_LT15_50            (PlanRegio) := Extra_aansluitingen/A12_Aansl_LT_LT15_50;
				attribute<nrAsl>        A13_Aansl_LT_LT15_70            (PlanRegio) := Extra_aansluitingen/A13_Aansl_LT_LT15_70;
				attribute<nrAsl>        A14_Aansl_LT_LT30_30            (PlanRegio) := Extra_aansluitingen/A14_Aansl_LT_LT30_30;
				attribute<nrAsl>        A15_Aansl_LT_LT30_50            (PlanRegio) := Extra_aansluitingen/A15_Aansl_LT_LT30_50;
				attribute<nrAsl>        A16_Aansl_LT_LT30_70            (PlanRegio) := Extra_aansluitingen/A16_Aansl_LT_LT30_70;
				attribute<nrAsl>        A17_Aansl_LT_TEO                (PlanRegio) := Extra_aansluitingen/A17_Aansl_LT_TEO;
				attribute<nrAsl>        A18_Aansl_LT_BuurtWKO           (PlanRegio) := Extra_aansluitingen/A18_Aansl_LT_BuurtWKO;
				attribute<nrAsl>        A19_Aansl_MT_Restwarmte         (PlanRegio) := Extra_aansluitingen/A19_Aansl_MT_Restwarmte;
				attribute<nrAsl>        A20_Aansl_MT_Geothermie         (PlanRegio) := Extra_aansluitingen/A20_Aansl_MT_Geothermie;
				attribute<nrAsl>        A21_Aansl_MT_WijkWKK            (PlanRegio) := Extra_aansluitingen/A21_Aansl_MT_WijkWKK;
				attribute<nrAsl>        A22_Aansl_MT_BioWKK             (PlanRegio) := Extra_aansluitingen/A22_Aansl_MT_BioWKK;
				attribute<nrAsl>        A23_Aansl_H2                    (PlanRegio) := Extra_aansluitingen/A23_Aansl_H2;
			
				unit<uint32> selected_Havenstad := range(uint32, 0, strcount(StudieGebied_Havenstad, ',') + 1 )
				{
					attribute<string> values := ReadArray(trim(StudieGebied_Havenstad), ., String, 0);
				}
				
				unit<uint32> Regio_selectie_Havenstad := ='subset('+AsList('PerPlanRegio/I01_buurtcode == ' +quote(selected_Havenstad/values), ' || ')+')'
				{
					attribute<String>       BU_CODE                     := ../BU_CODE[nr_OrgEntity];
					attribute<String>       I01_buurtcode               := ../I01_buurtcode[nr_OrgEntity];
					attribute<String>       I02_buurtnaam               := ../I02_buurtnaam[nr_OrgEntity];
					attribute<String>       I03_wijkcode                := ../I03_wijkcode[nr_OrgEntity];
					attribute<String>       I04_wijknaam                := ../I04_wijknaam[nr_OrgEntity];
					attribute<String>       I05_gemeentecode            := ../I05_gemeentecode[nr_OrgEntity];
					attribute<String>       I06_gemeentenaam            := ../I06_gemeentenaam[nr_OrgEntity];
					attribute<String>       I07_energieregionaam        := ../I07_energieregionaam[nr_OrgEntity];
					attribute<String>       I08_provincienaam           := ../I08_provincienaam[nr_OrgEntity];
					attribute<nrAsl>        I09_aantal_woningen	        := ../I09_aantal_woningen[nr_OrgEntity];
					attribute<nrAsl>        I10_aantal_utiliteit        := ../I10_aantal_utiliteit[nr_OrgEntity];
					attribute<nrWoning>     I11_woningequivalenten      := ../I11_woningequivalenten[nr_OrgEntity];
					attribute<string>       Uitgesloten                 := ../Uitgesloten[nr_OrgEntity];
				
					attribute<GJ_yr_WEQ>    H01_Vraag_Totaal            := ../H01_Vraag_Totaal[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H02_Vraag_Ruimteverwarming  := ../H02_Vraag_Ruimteverwarming[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H03_Vraag_Tapwater          := ../H03_Vraag_Tapwater[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H04_Vraag_Ventilatie        := ../H04_Vraag_Ventilatie[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H05_Vraag_Koude             := ../H05_Vraag_Koude[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H06_Vraag_Apparaten         := ../H06_Vraag_Apparaten[nr_OrgEntity];
					attribute<GJ_yr_ha>     H07_Vraag_perhectare        := ../H07_Vraag_perhectare[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H08_Input_Totaal            := ../H08_Input_Totaal[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H09_Input_Aardgas           := ../H09_Input_Aardgas[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H10_Input_Duurzaamgas       := ../H10_Input_Duurzaamgas[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H11_Input_Elektriciteit     := ../H11_Input_Elektriciteit[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H12_Input_MTwarmte          := ../H12_Input_MTwarmte[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H13_Input_LTwarmte          := ../H13_Input_LTwarmte[nr_OrgEntity];
					attribute<GJ_yr_WEQ>    H14_Saldo_Omgevingswarmte   := ../H14_Saldo_Omgevingswarmte[nr_OrgEntity];
					attribute<ton_yr>       H15_CO2_uitstoot            := ../H15_CO2_uitstoot[nr_OrgEntity];
					attribute<Eur_yr>       H16_Nat_meerkost            := ../H16_Nat_meerkost[nr_OrgEntity];
					attribute<Eur_ton>      H17_Nat_meerkost_CO2        := ../H17_Nat_meerkost_CO2[nr_OrgEntity];
					attribute<Eur_yr_WEQ>   H18_Nat_meerkost_WEQ        := ../H18_Nat_meerkost_WEQ[nr_OrgEntity];
					attribute<Eur_m3>       H19_Waarde_hernieuwbaar_gas := ../H19_Waarde_hernieuwbaar_gas[nr_OrgEntity];
					
					attribute<nrAsl>        A01_Aansl_aardgas           := ../A01_Aansl_aardgas[nr_OrgEntity];
					attribute<nrAsl>        A02_Aansl_eWP               := ../A02_Aansl_eWP[nr_OrgEntity];
					attribute<nrAsl>        A03_Aansl_MT                := ../A03_Aansl_MT[nr_OrgEntity];
					attribute<nrAsl>        A04_Aansl_LT                := ../A04_Aansl_LT[nr_OrgEntity];
					attribute<nrAsl>        A05_Aansl_hWP_HG            := ../A05_Aansl_hWP_HG[nr_OrgEntity];
					attribute<nrAsl>        A06_Aansl_HR_HG             := ../A06_Aansl_HR_HG[nr_OrgEntity];
			
					attribute<nrAsl>        A10_Aansl_LT_WKO            := ../A10_Aansl_LT_WKO[nr_OrgEntity];
					attribute<nrAsl>        A11_Aansl_LT_LT15_30        := ../A11_Aansl_LT_LT15_30[nr_OrgEntity];
					attribute<nrAsl>        A12_Aansl_LT_LT15_50        := ../A12_Aansl_LT_LT15_50[nr_OrgEntity];
					attribute<nrAsl>        A13_Aansl_LT_LT15_70        := ../A13_Aansl_LT_LT15_70[nr_OrgEntity];
					attribute<nrAsl>        A14_Aansl_LT_LT30_30        := ../A14_Aansl_LT_LT30_30[nr_OrgEntity];
					attribute<nrAsl>        A15_Aansl_LT_LT30_50        := ../A15_Aansl_LT_LT30_50[nr_OrgEntity];
					attribute<nrAsl>        A16_Aansl_LT_LT30_70        := ../A16_Aansl_LT_LT30_70[nr_OrgEntity];
					attribute<nrAsl>        A17_Aansl_LT_TEO            := ../A17_Aansl_LT_TEO[nr_OrgEntity];
					attribute<nrAsl>        A18_Aansl_LT_BuurtWKO       := ../A18_Aansl_LT_BuurtWKO[nr_OrgEntity];
					attribute<nrAsl>        A19_Aansl_MT_Restwarmte     := ../A19_Aansl_MT_Restwarmte[nr_OrgEntity];
					attribute<nrAsl>        A20_Aansl_MT_Geothermie     := ../A20_Aansl_MT_Geothermie[nr_OrgEntity];
					attribute<nrAsl>        A21_Aansl_MT_WijkWKK        := ../A21_Aansl_MT_WijkWKK[nr_OrgEntity];
					attribute<nrAsl>        A22_Aansl_MT_BioWKK         := ../A22_Aansl_MT_BioWKK[nr_OrgEntity];
					attribute<nrAsl>        A23_Aansl_H2                := ../A23_Aansl_H2[nr_OrgEntity];
				}
			}
			
			container Extra_aansluitingen : using = "/LeidraadResultaten/ZichtJaar/PlanregioResults"
			{
				attribute<nrAsl> A10_Aansl_LT_WKO (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/WKO,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A11_Aansl_LT_LT15_30 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT15_30,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A12_Aansl_LT_LT15_50 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT15_50,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A13_Aansl_LT_LT15_70 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT15_70,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A14_Aansl_LT_LT30_30 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT30_30,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A15_Aansl_LT_LT30_50 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT30_50,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A16_Aansl_LT_LT30_70 (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/LT30_70,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A17_Aansl_LT_TEO (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/TEO,0[nrAsl]))[nrAsl];
							
				attribute<nrAsl> A18_Aansl_LT_BuurtWKO (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/BuurtWKO,0[nrAsl]))[nrAsl];
			
				attribute<nrAsl> A19_Aansl_MT_Restwarmte (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/Restwarmte + WarmteBestaand/nrAansl_w_MT + WarmteBestaand/nrAansl_u_MT ,0[nrAsl]))[nrAsl];
								
				attribute<nrAsl> A20_Aansl_MT_Geothermie (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/Geothermie,0[nrAsl]))[nrAsl];
								
				attribute<nrAsl> A21_Aansl_MT_WijkWKK (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/WijkWKK,0[nrAsl]))[nrAsl];
			
				attribute<nrAsl> A22_Aansl_MT_BioWKK (PlanRegio) :=
					round(Statisch/IsUitgesloten
							? 0[nrAsl]
							: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/BioWKK,0[nrAsl]))[nrAsl];
			
				attribute<nrAsl> A23_Aansl_H2 (PlanRegio) :=
					round((Statisch/IsUitgesloten || !GroenGas)
								? 0[nrAsl]
								: MakeDefined(R_AardG * StateNaAllocatie/nrAansluitinging/WaterstofHR,0[nrAsl]))[nrAsl];
			}

			unit<uint32> list_PerPlanRegio          := SubItem_PropValues(PerPlanRegio/Regio_selectie_Havenstad,'name');
			
			container export_PerPlanRegio_Havenstad := model/CalculationSchemes/Rapportage/WriteTable32ToCSV_ESDL(PerPlanRegio/Regio_selectie_Havenstad, AsList(list_PerPlanRegio/name, ';') , FolderInfo/OutputFolder+'/PerPlanRegio_ESDL.csv');
		}
	}
	
	

	
	container StartJaar := CalculationSchemes/LeidraadResultaten('StartJaar')
	{
		container WarmteBestaand
		{
			attribute<ratio>  R_StadV         (PlanRegio) := Invoer/RuimtelijkeData/StudieGebied/buurt/R_StadV;

			attribute<GJ_yr>  V_Bijstook      (PlanRegio) := R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Bijstook;
			attribute<GJ_yr>  V_wd_elek       (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/V_wd_elek;

			attribute<GJ_yr>  V_ow_warmte     (PlanRegio) :=
				  R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_WarmteVraag
				+ R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Verlies
				- V_Bijstook
				+ R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_LeidingVerlies;

			attribute<Eur_yr> Kj_ow_productie (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/Iterations/I0/Assignment/Km_WarmteFactor * GebiedsAllocatie/RestWarmte/KostenBaten/V_WarmtePrimairD;
			attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/Km_wd_gas;
			attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/Km_wd_elek;

			attribute<GJ_yr>  V_elek_w        (PlanRegio) := R_StadV * sum(TussenResultaten/AllocatieResultaten/BestaandeWoning/metervraag/Elektriciteit, bebouwing/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>  V_elek_u        (PlanRegio) := R_StadV * sum(TussenResultaten/AllocatieResultaten/BestaandeUtil/metervraag/Elektriciteit, bebouwing/BestaandeUtil/PlanRegio_rel);

			attribute<Eur_yr> Kj_lv_admin     (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/Opbrengsten/jaarlijksD/Oj_vastrecht * TussenResultaten/Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_lv_Admin * 1[yr];

			attribute<Eur_yr> Kj_id_oh        (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_id_oh;
			attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_wd_oh;
			attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_wd_adm;

			attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_pt_oh;
			attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_pt_adm;
			attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_ow_oh;
			attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_ow_adm;
			attribute<Eur_yr> Kj_gnet_oh      (PlanRegio) := R_StadV * Netwerken/Aardgas/Kj_oh;
			attribute<Eur_yr> Kj_enet_oh      (PlanRegio) := R_StadV * Netwerken/Elektriciteit/Kj_oh;
			attribute<nrAsl>  nrAansl_w_MT    (PlanRegio) := R_StadV * sum(StateNaAllocatie/bebouwing/BestaandeWoning/nrAansluitingen, StateNaAllocatie/bebouwing/BestaandeWoning/PlanRegio_rel);
			attribute<nrAsl>  nrAansl_u_MT    (PlanRegio) := R_StadV * sum(StateNaAllocatie/bebouwing/BestaandeUtil/nrAansluitingen  , StateNaAllocatie/bebouwing/BestaandeUtil/PlanRegio_rel  );
			attribute<Eur_yr> Kj_elek_gebouw  (PlanRegio) := R_StadV * Maatschappelijk/gebouwen/ElektriciteitsVraag/Km_Totaal;
		}
		container OrigKosten
		{
			container table := Ref2030/PlanregioResults/NationaleKosten/Table
			{
				attribute<Eur_yr> K01_Enet_verzwaren         (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K02_Gnet_verwijderen       (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K03_Gnet_aanpassen         (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K04_Wnet_buurt             (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K05_Wnet_pand              (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K06_Wnet_transport         (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K07_Wnet_opwekking         (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K08_Gebouw_schil           (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K09_Gebouw_installatie     (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K10_Totaal_Kapitaalslasten (PlanRegio) := const(0[Eur_yr], PlanRegio);
			}
			container TotaalUitstoot
			{
				attribute<Eur_yr> Natkosten_abs              (Planregio) := const(0[Eur_yr], PlanRegio);
			}
			attribute<ton_yr> Orig_CO2_ton (Planregio) :=
				  StartJaar/EnergyResults/CO2/gebouwen/Aardgas/totaal / 1000.0[kg_yr / ton_yr]
				+ StartJaar/EnergyResults/CO2/GebiedsOpties/totaal    / 1000.0[kg_yr / ton_yr];
		}
	}
	container Ref2030        := CalculationSchemes/LeidraadResultaten(Classifications/Rekenstap/name[uint8(1)])
	{	
		container WarmteBestaand
		{
			attribute<ratio>  R_StadV         (PlanRegio) := Invoer/RuimtelijkeData/StudieGebied/buurt/R_StadV;

			attribute<GJ_yr>  V_Bijstook      (PlanRegio) := R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Bijstook;

			attribute<GJ_yr>  V_wd_elek       (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/V_wd_elek;

			attribute<GJ_yr> V_ow_warmte  (PlanRegio) :=
				  R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_WarmteVraag
				+ R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Verlies
				- V_Bijstook
				+ R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_LeidingVerlies;

			attribute<Eur_yr> Kj_ow_productie (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/Iterations/I0/Assignment/Km_WarmteFactor * GebiedsAllocatie/RestWarmte/KostenBaten/V_WarmtePrimairD;
			attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/Km_wd_gas;
			attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/Km_wd_elek;

			attribute<GJ_yr>  V_elek_w        (PlanRegio) := R_StadV * sum(Allocatie/BestaandeWoning/metervraag/Elektriciteit, bebouwing/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>  V_elek_u        (PlanRegio) := R_StadV * sum(Allocatie/BestaandeUtil/metervraag/Elektriciteit, bebouwing/BestaandeUtil/PlanRegio_rel);

			attribute<Eur_yr> Kj_lv_admin     (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/KostenBaten/Opbrengsten/jaarlijksD/Oj_vastrecht * TussenResultaten/Leercurves/OenM/Curve * Kengetallen/Onderhoud/R_lv_Admin * 1[yr];

			attribute<Eur_yr> Kj_id_oh        (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_id_oh;
			attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_wd_oh;
			attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := R_StadV * StartJaar/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Kj_wd_adm;

			attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_pt_oh;
			attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_pt_adm;
			attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_ow_oh;
			attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := R_StadV * Startjaar/GebiedsAllocatie/RestWarmte/lastiter/Assignment/Kj_ow_adm;
			attribute<Eur_yr> Kj_gnet_oh      (PlanRegio) := R_StadV * Netwerken/Aardgas/Kj_oh;
			attribute<Eur_yr> Kj_enet_oh      (PlanRegio) := R_StadV * Netwerken/Elektriciteit/Kj_oh;

			attribute<nrAsl>nrAansl_w_MT      (PlanRegio) := R_StadV * sum(StateNaAllocatie/bebouwing/BestaandeWoning/nrAansluitingen, StateNaAllocatie/bebouwing/BestaandeWoning/PlanRegio_rel);
			attribute<nrAsl>nrAansl_u_MT      (PlanRegio) := R_StadV * sum(StateNaAllocatie/bebouwing/BestaandeUtil/nrAansluitingen  , StateNaAllocatie/bebouwing/BestaandeUtil/PlanRegio_rel  );
			attribute<Eur_yr> Kj_elek_gebouw  (PlanRegio) := R_StadV * Maatschappelijk/gebouwen/ElektriciteitsVraag/Km_Totaal;
		}

		container OrigKosten := Startjaar/OrigKosten
		{
			container table := Startjaar/OrigKosten/table
			{
				attribute<Eur_yr> K11_Inkoop_Warmte  (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K12_Inkoop_Gas     (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K13_Inkoop_Elek    (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K14_Gebouw_OenM    (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K15_Wnet_OenM      (PlanRegio) := const(0[Eur_yr], PlanRegio);
				attribute<Eur_yr> K16_Enet_Gnet_OenM (PlanRegio) := const(0[Eur_yr], PlanRegio);
			}
		}
	}

	container ZichtJaar := CalculationSchemes/LeidraadResultaten(last(Classifications/Rekenstap/name))
	{
		container OrigKosten     := Ref2030/PlanregioResults/NationaleKosten
		{
			container Table := Ref2030/PlanregioResults/NationaleKosten/Table
			{
				attribute<Eur_yr> K09_Gebouw_installatie     (PlanRegio) := Ref2030/PlanregioResults/NationaleKosten/Recap/K09_Gebouw_installatie;
				attribute<Eur_yr> K10_Totaal_Kapitaalslasten (PlanRegio) := Ref2030/PlanregioResults/NationaleKosten/Recap/K09_Gebouw_installatie;
			}
			attribute<ton_yr> Orig_CO2_ton (Planregio) := Ref2030/PlanregioResults/NationaleKosten/TotaalUitstoot/New_CO2_ton;
		}

		//TEST
		container Warmtebestaand := Ref2030/WarmteBestaand
		{
			attribute<ratio>  R_StadV         (PlanRegio) := Invoer/RuimtelijkeData/StudieGebied/buurt/R_StadV;
			attribute<GJ_yr>  V_Bijstook      (PlanRegio) := R_StadV * (1d - ..../TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * ..../TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Bijstook;
			attribute<GJ_yr>  V_wd_elek       (PlanRegio) := R_StadV * ..../TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/V_wd_elek;
			attribute<GJ_yr>  V_elek_w        (PlanRegio) := R_StadV * sum(..../TussenResultaten/R2_2030/AllocatieResultaten/BestaandeWoning/metervraag/Elektriciteit, bebouwing/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>  V_elek_u        (PlanRegio) := R_StadV * sum(..../TussenResultaten/R2_2030/AllocatieResultaten/BestaandeUtil/metervraag/Elektriciteit, bebouwing/BestaandeUtil/PlanRegio_rel);

			//attribute<GJ_yr>  V_ow_warmte     (PlanRegio) := R_StadV * ..../TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_warmte;

			attribute<GJ_yr>  V_ow_warmte     (PlanRegio) :=
				  R_StadV * /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_WarmteVraag
				+ R_StadV * (1d - /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol) * /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/V_Verlies
				- V_Bijstook
				+ R_StadV * /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/V_LeidingVerlies;

			attribute<Eur_yr> Kj_ow_productie (PlanRegio) := R_StadV * GebiedsAllocatie/RestWarmte/Iterations/I0/Assignment/Km_WarmteFactor * /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/V_WarmtePrimairD;
			attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := R_StadV * (1d - GebiedsAllocatie/RestWarmte/results/RestWarmte/P_vol)          * /TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/Bijstook/Km_wd_gas;
			attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := R_StadV * ..../TussenResultaten/R2_2030/GebiedsAllocatie/RestWarmte/KostenBaten/KostenD/SecundairNet/Km_wd_elek;
		}
		//TEST
	}
}