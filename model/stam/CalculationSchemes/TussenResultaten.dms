//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container tussenresultaten
{
	#include <RekenStapT.dms>
	#include <BebouwingsComponentT.dms>
	#include <GebouwOptieT.dms>
	
	template DoelLabelT {
		unit<uint32> BebouwingsObject;
		parameter<Classifications/DoelLabel_NaSprong> DoelLabel_rel;
		
		unit<uint8>  SchilSprong_set := VerbruiksOpties/SchilSprong_set; // outward ref
		unit<uint32> SchilSprong_x_MO:= VerbruiksOpties/SchilSprong_set/xMO;
		
		attribute<Classifications/SchilLabel> Van_rel(BebouwingsObject) := BebouwingsObject/schillabel_rel;
		parameter<Classifications/SchilLabel> Naar_rel                  := Classifications/DoelLabel_NaSprong/SchilLabel_rel[DoelLabel_rel];
		
		attribute<SchilSprong_set > SchilSprong_rel    (BebouwingsObject) := rlookup(combine_data(Classifications/SchilSprongCode, Van_rel, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel(BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel, BebouwingsObject/Model_rel);
		
		attribute<EUR> Ki_GV (BebouwingsObject) := MakeDefined(
			SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel] * BebouwingsObject/nrAansluitingen +
			SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel] * BebouwingsObject/Oppervlakte    
		,	0[EUR]
		);
	}
		
	template WarmteVraagT // TODO: heroverwegen per gebiedsoptie. Kijk eerst waar dit gebruikt wordt. REMOVE?
	{
		unit<uint32> BebouwingsObject;
		attribute<bool> KanGebiedsoptie (BebouwingsObject) := Classifications/WarmteOptie/kanGebiedsOptie[BebouwingsObject/WarmteOptie_rel];
		
		 // TODO: REMOVE AandeelWarmteVraag obv aanname dan Deelname altijd 1.0 is en verwijder specificatie van die Deelname
		 // TODO: dan consider wegsubstitueren van deze template
		container deelnemers
		{
			attribute<nrAansl>   NrAansluitingen    (BebouwingsObject) := float64(KanGebiedsoptie) * BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/nrAansluitingen;
			attribute<m2>        Oppervlakte        (BebouwingsObject) := float64(KanGebiedsoptie) * BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/Oppervlakte;
			attribute<kW>        AansluitCapaciteit (BebouwingsObject) := float64(KanGebiedsoptie) * BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/AansluitCapaciteit/Warmte;
			attribute<GJ_yr>     Verbruik_RV        (BebouwingsObject) := float64(KanGebiedsoptie) * BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/Functioneel/Vj_RV;
			attribute<GJ_yr>     Verbruik_TW        (BebouwingsObject) := float64(KanGebiedsoptie) * BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/Functioneel/Vj_TW;
			attribute<GJ_yr>     Verbruik           (BebouwingsObject) := Verbruik_RV + Verbruik_TW;
		}

		container alle
		{
			attribute<nrAansl>   nrAansluitingen    (BebouwingsObject) := BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/nrAansluitingen;
			attribute<kW>        AansluitCapaciteit (BebouwingsObject) := BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/AansluitCapaciteit/Warmte;
			attribute<GJ_yr>     Verbruik           (BebouwingsObject) := BebouwingsObject/AandeelWarmteVraag * (BebouwingsObject/Functioneel/Vj_RV + BebouwingsObject/Functioneel/Vj_TW);
			attribute<m2>        Oppervlakte        (BebouwingsObject) := BebouwingsObject/AandeelWarmteVraag * BebouwingsObject/Oppervlakte;
		}
	}
}