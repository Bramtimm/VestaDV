//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Prijzen
{
	container AardGas
	{
		unit<uint32> Tabel := =IsGroengas ? 'invoer/energieprijzen/GroenGas/Table'   : 'invoer/energieprijzen/AardGas/Table';
		container bron     := =IsGroengas ? 'invoer/energieprijzen/GroenGas/results' : 'invoer/energieprijzen/AardGas/results';
		
		parameter<Tabel> JaarIndex := classify(RekenJaar_jaar, bron/Jaar);
		parameter<KG_GJ> CO2_GJ    := bron/CO2_GJ[JaarIndex] * (1.0 - Voorkeuren/GroenGasFactor);
		parameter<bool>  IsGroengas:= Voorkeuren/Afweging/IsGroenGas;
		
		unit<uint8> Staffel := classifications/gebruiksgrootteklasse 
		{
			attribute<GJ_yr>  ClassBreak := gas_break;
			attribute<Eur_GJ> KGJ_Commodity        := ='union_data(., ' + AsItemList('bron/' + Name + '/CommodityPrice  [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_netwerk          := ='union_data(., ' + AsItemList('bron/' + Name + '/netwerk         [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_eindgebruik_excl := ='union_data(., ' + AsItemList('bron/' + Name + '/eindgebruik_excl[JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_maatschappelijk  := ='union_data(., ' + AsItemList('bron/' + Name + '/maatschappelijk [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_EnergieHeffing   := ='union_data(., ' + AsItemList('bron/' + Name + '/EnergieHeffing  [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_CO2Heffing       := ='union_data(., ' + AsItemList('bron/' + Name + '/CO2Heffing      [JaarIndex]') + ')';
		}

		unit<uint8>  Glastuinbouw : nrofrows = 1
		{
			attribute<GJ_yr>  ClassBreak           := union_data(., first(staffel/Classbreak));
			attribute<Eur_GJ> KGJ_eindgebruik_excl := bron/Glastuinbouw/eindgebruik_excl[const(JaarIndex,.)];
			attribute<Eur_GJ> KGJ_maatschappelijk  := bron/Glastuinbouw/maatschappelijk [const(JaarIndex,.)];
			attribute<Eur_GJ> KGJ_netwerk          := bron/Glastuinbouw/netwerk         [const(JaarIndex,.)];
			attribute<Eur_GJ> KGJ_EnergieHeffing   := bron/Glastuinbouw/EnergieHeffing  [const(JaarIndex,.)];
			attribute<Eur_GJ> KGJ_CO2Heffing       := bron/Glastuinbouw/CO2Heffing      [const(JaarIndex,.)];
		}

		container Bijstook := bron/GrootGebruik
		{
			parameter<Eur_GJ> KGJ_eindgebruik_excl := eindgebruik_excl[JaarIndex];
			parameter<Eur_GJ> KGJ_maatschappelijk  := maatschappelijk [JaarIndex];
			parameter<Eur_GJ> KGJ_netwerk          := netwerk         [JaarIndex];
			parameter<Eur_GJ> KGJ_CO2Heffing       := CO2Heffing      [JaarIndex];
			parameter<Eur_GJ> KGJ_EnergieHeffing   := EnergieHeffing  [JaarIndex];
		}
	}

	container Elektriciteit
	{
		unit<uint32> Tabel := invoer/energieprijzen/Elektriciteit/Table;
		container    bron  := invoer/energieprijzen/Elektriciteit/results;

		parameter<Tabel> JaarIndex := classify(RekenJaar_jaar, bron/Jaar);
		parameter<KG_GJ> CO2_GJ    := bron/CO2_GJ[JaarIndex];

		unit<uint8> Staffel := classifications/gebruiksgrootteklasse
		{
			attribute<GJ_yr>  ClassBreak           := elek_break_kWh [GJ_yr];
			attribute<Eur_GJ> KGJ_commodity        := ='union_data(., ' + AsItemList('bron/' + Name + '/CommodityPrice  [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_eindgebruik_excl := ='union_data(., ' + AsItemList('bron/' + Name + '/eindgebruik_excl[JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_maatschappelijk  := ='union_data(., ' + AsItemList('bron/' + Name + '/maatschappelijk [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_netwerk          := ='union_data(., ' + AsItemList('bron/' + Name + '/netwerk         [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_EnergieHeffing   := ='union_data(., ' + AsItemList('bron/' + Name + '/EnergieHeffing  [JaarIndex]') + ')';
			attribute<Eur_GJ> KGJ_CO2Heffing       := ='union_data(., ' + AsItemList('bron/' + Name + '/CO2Heffing      [JaarIndex]') + ')';
		}
	}
	
	// TODO: van biomassa en pellets prijzen en maatschappelijke kosten onderscheiden zoals bij gas en elektra.
	container overig {
		container    bron  := invoer/energieprijzen/Overig/results;
	
		parameter<EUR_GJ> Pj_biomassa := bron/Biomassa [rlookup(RekenJaar_jaar, bron/jaar)];
		parameter<EUR_GJ> Pj_pellets  := bron/Pellets  [rlookup(RekenJaar_jaar, bron/jaar)];
		parameter<EUR_GJ> Pj_H2       := bron/Waterstof[rlookup(RekenJaar_jaar, bron/jaar)];
		
		parameter<EUR_GJ> Pm_biomassa := Pj_biomassa;
		parameter<EUR_GJ> Pm_pellets  := Pj_pellets;
	}
}